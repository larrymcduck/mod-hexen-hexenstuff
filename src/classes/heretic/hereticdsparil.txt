Actor HereticDsparilClass : MonsterClass
{
	Player.StartItem "IsHereticMonster"
	Player.ColorRange 164, 185
	Player.ViewHeight 64
	Player.DisplayName "DSparil"
	Player.StartItem "MonsterHealth", 100
	Player.ForwardMove 0.7, 0.7
	Player.SideMove 0.7, 0.7
	Mass 500
	Player.JumpZ 9
	Player.Hexenarmor 50, 35, 30, 25, 15
	PainSound "dsparil/pain"
	Player.SoundClass "hereticdsparil"
	Player.StartItem "HereticDsparilWeapon"
	Player.WeaponSlot 1, HereticDsparilWeapon
	Player.Face "DSP"

	States
	{
		Spawn:
			SOR2 M -1
			stop
		See:
			SOR2 MNOP 8
			loop
		Melee:
		Missile:
			SOR2 RST 8
			Goto Spawn
		Pain:
			SOR2 Q 8 A_Pain
			Goto Spawn
		Death:
			SDTH A 8 A_Scream
			SDTH B 8 A_NoBlocking
			SDTH C 8
			SDTH DE 7
			SDTH F 7
			SDTH G 6 A_PlaySound("dsparil/explode")
			SDTH H 6
			SDTH I 18
			SDTH J 6
			SDTH K 6 A_PlaySound("dsparil/bones")
			SDTH LMN 6
			SDTH O -1
			Stop
			
		Death.MonsterIce:
		XDeath.MonsterIce:
			FDMN A 0 A_GiveToTarget("Health", 50)
			FDMN A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Ice
		Death.Monster:
		Death.MonsterFire:
		XDeath.MonsterFire:
		XDeath.Monster:
			FDMN A 0 A_GiveToTarget("Health", 50)
			FDMN A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Death
	}
}

Actor HereticDsparilWeapon : Weapon
{
	Inventory.RestrictedTo "HereticDsparilClass"
	States
	{
		Spawn:
			TNT1 A 1
			stop
		Select:
			DSPS A 1 A_Raise
			loop
		Deselect:
			DSPS A 1 A_Lower
			loop
		Ready:
			DSPS A 1 A_WeaponReady
			loop
		Fire:
			DSPS A 4 Bright A_PlaySound("dsparil/attack")
			DSPS B 4 Bright
			DSPS C 8 Bright Offset (0, 48) A_FireCustomMissile("Sorcerer2FX1_2", 0, 0, 0, 24)
			DSPS B 8 Bright Offset (0, 40)
			DSPS A 8 Bright
			Goto Ready
		AltFire:
			DSPS A 0 A_JumpIfInventory("DiscipleSummons",1,"Summon")
			Goto Ready
		Summon:
			DSPS A 4 Bright A_PlaySound("dsparil/attack")
			DSPS B 4 Bright
			DSPS C 0 A_TakeInventory("DiscipleSummons", 1)
			DSPS C 8 Bright Offset (0, 48) A_FireCustomMissile("Sorcerer2FX2_2", 0, 0, 0, 24)
			DSPS B 8 Bright Offset (0, 40)
			DSPS A 8 Bright
			Goto Ready
	}
}

Actor DiscipleSummons : Inventory
{
	Inventory.MaxAmount 10
}

Actor Sorcerer2FX2_2 : Sorcerer2FX2
{
	States
	{
		Spawn:
			SKST G 35 Bright
			SKST G 5 Bright A_GenWizard
			SKST H 5 Bright
			Goto Spawn+1
		Death:
			SKST IJKLM 5 Bright
			stop
	}
}

Actor WizardFX1_2 : WizardFX1 replaces WizardFX1
{
	DamageType "Monster"
	Damage 5
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	States
	{
		Spawn:
			SKST GH 6 Bright
			loop
		Death:
			SKST IJKLM 5 Bright
			stop
	}
}

Actor Sorcerer2FX1_2 : Sorcerer2FX1
{
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	Damage 12
	DamageType "Monster"
	States
	{
		Death:
			FX16 GHIJKL 5 Bright
			Stop
	}
}