Actor HereticSnakeMonsterClass : MonsterClass
{
	Player.StartItem "IsHereticMonster"
	Player.ColorRange 164, 185
	Player.ViewHeight 56
	Player.DisplayName "Serpentine"
	Player.StartItem "MonsterHealth", 100
	Player.ForwardMove 0.8, 0.8
	Player.SideMove 0.8, 0.8
	Player.JumpZ 9
	PainSound "snake/pain"
	Player.SoundClass "hereticsnake"
	Player.StartItem "HereticSnakeWeapon"
	Player.StartItem "MonsterPowerAttack"
	Player.WeaponSlot 1, HereticSnakeWeapon
	Player.Face "SNK"
	
	States
	{
		Spawn:
			SNKE A -1
			stop
		See:
			SNKE ABCD 4
			loop
		Melee:
		Missile:
			SNKE F 33
			Goto Spawn
		Pain:
			SNKE E 8 A_Pain
			Goto Spawn
		Death:
			SNKE G 5 A_PlayerScream
			SNKE H 5 A_NoBlocking
			SNKE IJKLMNO 5
			SNKE P 5 A_SpawnItemEx("ReviveSnake")
			TNT1 A -1
			stop
			
		Death.MonsterIce:
		XDeath.MonsterIce:
			FDMN A 0 A_GiveToTarget("Health", 25)
			FDMN A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Ice
		Death.Monster:
		Death.MonsterFire:
		XDeath.MonsterFire:
		XDeath.Monster:
			FDMN A 0 A_GiveToTarget("Health", 25)
			FDMN A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Death
	}
}

Actor HereticSnakeWeapon : Weapon
{
	Inventory.RestrictedTo "HereticSnakeMonsterClass"
	States
	{
		Spawn:
			TNT1 A 1
			stop
		Select:
			SNFX P 1 A_Raise
			loop
		Deselect:
			SNFX P 1 A_Lower
			loop
		Ready:
			SNFX P 1 A_WeaponReady
			loop
		Fire:
			SNFX Q 2
			SNFX RRR 4 A_FireCustomMissile("SnakeProjA2")
			SNFX Q 15
			SNFX R 4 A_FireCustomMissile("SnakeProjB2")
		WaitFire:
			SNFX Q 15
			Goto Ready

		AltFire:
			SNFX Q 2
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 10, "Level4")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 6, "Level3")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 3, "Level2")
			Goto Fire+1
		Level2:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 3)
			SNFX RRR 4 A_FireCustomMissile("SnakeProjB2")
			SNFX Q 15
			SNFX R 4 A_FireCustomMissile("SnakeProjC")
			Goto WaitFire
		Level3:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 6)
			SNFX RRR 4 A_FireCustomMissile("SnakeProjC")
			SNFX Q 15
			SNFX R 4 A_FireCustomMissile("SnakeProjD")
			Goto WaitFire
		Level4:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 10)
			SNFX RRR 4 A_FireCustomMissile("SnakeProjD")
			SNFX Q 15
			SNFX R 4 A_FireCustomMissile("SnakeProjE")
			Goto WaitFire
	}
}

Actor SnakeProjA2 : SnakeProjA
{
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	Damage 5
	DamageType "MonsterIce"
}

Actor SnakeProjB2 : SnakeProjB
{
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	Damage 15
	DamageType "MonsterFire"	
}

Actor SnakeProjC : SnakeProjB
{
	Damage 20
	DamageType "Monster"
	
	States
	{
		Spawn:
			SBS4 ABC 4 Bright
			Loop
		Death:
			SBFX ABCDEFG 4 Bright
			Stop
	}
}

Actor SnakeProjD : SnakeProjC
{
	Damage 30
	DamageType "MonsterFire"
	
	States
	{
		Spawn:
			SBS1 ABCD 4 Bright
			Loop
		Death:
			MSP2 EFGHI 4 Bright
			Stop
	}
}

Actor SnakeProjE : SnakeProjD
{
	Damage 40
	
	States
	{
		Spawn:
			MSP1 ABCD 4 Bright
			Loop
		Death:
			MSP1 GHIJKLMNOP 3 Bright
			Stop
	}
}