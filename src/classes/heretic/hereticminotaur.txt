Actor HereticMinotaurClass : MonsterClass
{
	Height 74
	Radius 18
	Player.StartItem "IsHereticMonster"
	Player.ColorRange 97, 121
	Player.ViewHeight 80
	Player.DisplayName "Maulotaur"
	Player.StartItem "MonsterHealth", 125
	Player.ForwardMove 0.4, 0.4
	Player.SideMove 0.4, 0.4
	Mass 500
	Player.JumpZ 10
	Player.Hexenarmor 60, 35, 30, 25, 15
	PainSound "minotaur/pain"
	Player.SoundClass "hereticminotaur"
	Player.StartItem "HereticMinotaurWeapon"
	Player.WeaponSlot 1, HereticMinotaurWeapon
	Player.Face "MIN"

	States
	{
		Spawn:
			MNTR A -1
			stop
		See:
			MNTR ABCD 5
			loop
		Melee:
			MNRT V 0 A_JumpIfInventory("MinotaurDash", 1, "Dash")
			MNTR V 10
			MNTR Y 7
			MNTR Z 12
			Goto Spawn
		Missile:
			MNRT V 0 A_JumpIfInventory("MinotaurDash", 1, "Dash")
			MNTR V 10
			MNTR W 4
			MNTR X 9
			Goto Spawn
		Dash:
			MNTR U 2
			Goto Spawn
		Pain:
			MNTR E 8 A_Pain
			Goto Spawn
		Death:
			MNTR E 6 A_PlayerScream
			MNTR E 2 A_NoBlocking
			MNTR E 5 A_SpawnItemEx("MinotaurSmokeExit")
			MNTR E 15
			MNTR E 5 A_SetTranslucent(0.66, 0)
			MNTR E 5 A_SetTranslucent(0.33, 0)
			MNTR E 5 A_SetTranslucent(0, 0)
			TNT1 A -1
			stop
			
		Death.MonsterIce:
		XDeath.MonsterIce:
			FDMN A 0 A_GiveToTarget("Health", 50)
			FDMN A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Ice
		Death.Monster:
		Death.MonsterFire:
		XDeath.MonsterFire:
		XDeath.Monster:
			FDMN A 0 A_GiveToTarget("Health", 50)
			FDMN A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Death
	}
}

Actor HereticMinotaurWeapon : Weapon
{
	Inventory.RestrictedTo "HereticMinotaurClass"
	States
	{
		Spawn:
			TNT1 A 1
			stop
		Select:
			MHAM A 1 A_Raise
			loop
		Deselect:
			MHAM A 1 A_Lower
			loop
		Ready:
			MHAM A 1 A_WeaponReady
			loop
		Fire:
			MHAM F 0 A_JumpIfInventory("MinotaurDash", 1, "TakeDash")
			MHAM B 0 A_JumpIfCloser(128, "Melee")
			MHAM B 0 Offset (30, 45) A_GunFlash
			MHAM B 2 Offset (30, 45)
			MHAM B 2 Offset (15, 45)
			MHAM B 2 Offset (0, 45)
			MHAM C 2 Offset (0, 45) A_CustomPunch (18,0,0,"MinotaurPuff",96)
			MHAM C 0 Offset (0, 45) A_FireCustomMissile("MinotaurFX1_2",-12, 0, 0, 32)
			MHAM C 0 Offset (0, 45) A_FireCustomMissile("MinotaurFX1_2",-6, 0, 0, 32)
			MHAM C 0 Offset (0, 45) A_FireCustomMissile("MinotaurFX1_2",0, 0, 0, 32)
			MHAM C 0 Offset (0, 45) A_FireCustomMissile("MinotaurFX1_2",6, 0, 0, 32)
			MHAM C 0 Offset (0, 45) A_FireCustomMissile("MinotaurFX1_2",12, 0, 0, 32)
			MHAM C 2 Offset (-15, 45)
			MHAM C 2 Offset (-30, 45)
			MHAM D 2 Offset (-30, 45)
			MHAM E 2 Offset (-35, 0)
			MHAM E 2 Offset (-35, 55)
			MHAM E 2 Offset (-35, 65)
			MHAM E 15 Offset (-35, 150)
			MHAM A 1 Offset (5, 60)
			MHAM A 1 Offset (5, 55)
			MHAM A 1 Offset (5, 50)
			MHAM A 1 Offset (5, 45)
			MHAM A 1 Offset (5, 40)
			MHAM A 1 Offset (5, 35)
			MHAM A 1
			Goto Ready
		Melee:
			MHAM F 0 A_JumpIfInventory("MinotaurDash", 1, "TakeDash")
			MHAM F 6 Offset (5, 0)
			MHAM G 3 Offset (5, 0) A_CustomPunch (10,0,0,"MinotaurPuff",128)
			MHAM H 3 Offset (5, 0)
			MHAM E 2 Offset (5, 0)
			MHAM E 10 Offset (5, 150) A_FireCustomMissile("MinotaurFX2_2")
			MHAM A 1 Offset (0, 60)
			MHAM A 1 Offset (0, 55)
			MHAM A 1 Offset (0, 50)
			MHAM A 1 Offset (0, 45)
			MHAM A 1 Offset (0, 40)
			MHAM A 1 Offset (0, 35)
			MHAM A 1
			Goto Ready
		TakeDash:
			MHAM A 0 A_TakeInventory("MinotaurDash", 1)
			Goto Ready
		AltFire:
			MHAM A 0 A_JumpIfCloser(128,"Melee")
			MHAM A 0 A_GiveInventory("MinotaurDash", 1)
			MHAM A 0 A_SpawnItemEx("MinotaurDashFlame")
			TNT1 A 1 A_Recoil (-2)
			MHAM A 0 A_ReFire
			MHAM A 0 A_TakeInventory("MinotaurDash", 1)
			Goto Ready			
	}
}

Actor MinotaurPuff : HammerPuff
{
	DamageType "Monster"
}

Actor MinotaurDash : Inventory
{
	Inventory.MaxAmount 1
}

Actor MinotaurFX1_2 : MinotaurFX1
{
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	Damage 5
	SeeSound "MaulatorHamHit"
	DeathSound "MaulatorMissileHit"
	DamageType "MonsterFire"
}

Actor MinotaurFX2_2 : MinotaurFX2
{
	SeeSound "ClericFlameFire"
	DeathSound "MaulatorMissileHit"
	DamageType "MonsterFire"
	ExplosionDamage 0
	
	States
	{
		Death:
			FX13 I 4 Bright A_Explode(128, 128, 0)
			FX13 JKLM 4 Bright
			Stop
	}
}

Actor MinotaurFX3_2 : MinotaurFX3 replaces MinotaurFX3
{
	ExplosionDamage 0
}

Actor MinotaurDashFlame
{	
	+FLOORHUGGER
	States
	{
		Spawn:
			FX13 DCBCDEFGH 4 Bright
			stop
	}		
}
