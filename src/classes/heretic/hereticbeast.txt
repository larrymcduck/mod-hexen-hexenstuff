Actor HereticBeastMonsterClass : MonsterClass
{
	Player.StartItem "IsHereticMonster"
	Player.ViewHeight 56
	Player.ColorRange 0, 66
	Player.DisplayName "Weredragon"
	Player.StartItem "MonsterHealth", 75
	Player.JumpZ 10
	PainSound "beast/pain"
	Player.SoundClass "hereticbeast"
	Player.StartItem "HereticBeastWeapon"
	Player.StartItem "MonsterPowerAttack"
	Player.WeaponSlot 1, HereticBeastWeapon
	Player.Face "BST"
	
	States
	{
		Spawn:
			BEAS A -1
			stop
		See:
			BEAS ABCDEF 4
			loop
		Melee:
		Missile:
			BEAS H 4
			BEAS I 10
			Goto Spawn
		Pain:
			BEAS G 8 A_Pain
			Goto Spawn
			
		Death.Monster:
		Death.MonsterFire:
		XDeath.MonsterFire:
			FDMN A 0 A_GiveToTarget("Health", 25)
			FDMN A 0 A_GiveToTarget("DiscipleSummons", 1)
		Death:
			BEAS R 6 A_PlayerScream
			BEAS S 6 A_NoBlocking
			BEAS TUVWXY 6
			BEAS Z 6 A_SpawnItemEx("ReviveBeast")
			TNT1 A -1
			stop
			
		XDeath.Monster:
			FDMN A 0 A_GiveToTarget("Health", 25)
			FDMN A 0 A_GiveToTarget("DiscipleSummons", 1)
		XDeath:
			BEAS J 5 A_PlayerScream
			BEAS K 5 A_NoBlocking
			BEAS LMNOP 5
			BEAS Q 5 A_SpawnItemEx("XReviveBeast")
			TNT1 A -1
			stop
			
		Death.MonsterIce:
		XDeath.MonsterIce:
			FDMN A 0 A_GiveToTarget("Health", 25)
			FDMN A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Ice
	}
}

Actor HereticBeastWeapon : Weapon
{
	Inventory.RestrictedTo "HereticBeastMonsterClass"
	States
	{
		Spawn:
			TNT1 A 1
			stop
		Select:
			TNT1 A 1 A_Raise
			loop
		Deselect:
			TNT1 A 1 A_Lower
			loop
		Ready:
			TNT1 A 1 A_WeaponReady
			loop
		Fire:
			TNT1 A 4
			TNT1 A 10 A_FireCustomMissile("BeastBall2")
		WaitFire:
			TNT1 A 10
			Goto Ready
			
		AltFire:
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 10, "Level4")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 6, "Level3")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 3, "Level2")
			Goto Fire
		Level2:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 3)
			TNT1 A 0 A_FireCustomMissile("BeastBall2", -3)
			TNT1 A 10 A_FireCustomMissile("BeastBall2", 3)
			Goto WaitFire
		Level3:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 6)
			TNT1 A 10 A_FireCustomMissile("BeastBall3")
			Goto WaitFire
		Level4:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 10)
			TNT1 A 0 A_FireCustomMissile("BeastBall3", -7)
			TNT1 A 0 A_FireCustomMissile("BeastBall3")
			TNT1 A 10 A_FireCustomMissile("BeastBall3", 7)
			Goto WaitFire
	}
}

Actor BeastBall2 : BeastBall
{
	Damage 10
	Speed 30
	DamageType "MonsterFire"
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
}

Actor BeastBall3 : BeastBall2
{
	Damage 30
	Translation "164:185=203:210"

	States
	{
		Spawn:
			MSP1 ABCD 4 Bright
			Loop
		Death:
			MSP1 G 4 Bright A_Explode(64, 128, 0)
			MSP1 HIJKLMNOP 4 Bright
			Stop
	}
}