Actor DragonMonsterClass : MonsterClass
{
	Radius 22
	Player.StartItem "IsHexenMonster"
	Player.ColorRange 97, 121
	Player.DisplayName "Death_Wyvern"
	PainSound "DragonPain"
	Player.SoundClass "dragon"
	Player.StartItem "DragonWeapon"
	Player.WeaponSlot 1, DragonWeapon
	Player.StartItem "MonsterFlight"
	Player.StartItem "MonsterHealth", 100
	Player.StartItem "MonsterPowerAttack"
	Player.ForwardMove 0.8, 0.8
	Player.SideMove 0.8, 0.8
	Player.Face "DGN"

	States
	{
		Spawn:
			DRAG ABCDCB 4 A_GiveInventory("MonsterFlight")
			loop
		See:
			DRAG ABCDCB 4
			loop
		Pain:
			DRAG F 8 A_Pain
			Goto Spawn
		Melee:
		Missile:
			DRAG E 15 Bright
			Goto Spawn
		Death:
			DRAG G 5 A_PlayerScream
			DRAG H 5 A_NoBlocking
			DRAG IJKL 5
			DRAG M -1
			stop
			
		Death.MonsterIce:
		XDeath.MonsterIce:
		Death.Monster:
		Death.MonsterFire:
		XDeath.Monster:
		XDeath.MonsterFire:
			SSPT A 0 A_GiveToTarget("Health", 25)
			SSPT A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Death
	}	
}

Actor DragonWeapon : Weapon
{
	Inventory.RestrictedTo "DragonMonsterClass"
	States
	{
		Spawn:
			TNT1 A 1
			stop
		Select:
			TNT1 A 1 A_Raise
			loop
		Deselect:
			TNT1 A 1 A_Lower
			loop
		Ready:
			TNT1 A 1 A_WeaponReady
			loop
		Fire:
			TNT1 AAAAA 4 A_FireCustomMissile("DragonFireball_2")
		WaitFire:
			TNT1 A 70
			Goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 10, "Level4")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 6, "Level3")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 3, "Level2")
			Goto Fire
		Level2:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 3)
			TNT1 AAAAA 4 A_FireCustomMissile("DragonFireball_3")
			Goto WaitFire
		Level3:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 6)
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-5, 5))
			TNT1 A 4 A_FireCustomMissile("DragonFireball_3", random(-5, 5))
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-5, 5))
			TNT1 A 4 A_FireCustomMissile("DragonFireball_3", random(-5, 5))
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-5, 5))
			TNT1 A 4 A_FireCustomMissile("DragonFireball_3", random(-5, 5))
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-5, 5))
			TNT1 A 4 A_FireCustomMissile("DragonFireball_3", random(-5, 5))
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-5, 5))
			TNT1 A 4 A_FireCustomMissile("DragonFireball_3", random(-5, 5))
			Goto WaitFire
		Level4:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 10)
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 4 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 4 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 4 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 4 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 0 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			TNT1 A 4 A_FireCustomMissile("DragonFireball_3", random(-10, 10))
			Goto WaitFire
	}
}

Actor DragonFireball_2 : DragonFireball
{
	Damage 10
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	DamageType "MonsterFire"
	States
	{
		Death:
			DRFX GHIJKL 4 Bright
			Stop
	}
}

Actor DragonFireball_3 : DragonFireball_2
{
	Damage 12
	States
	{
		Death:
			DRFX GHI 4 Bright
			DRFX J 4 A_DragonFX2
			DRFX KL 3 Bright
			Stop
	}
}

Actor DragonExplosion_2 : DragonExplosion replaces DragonExplosion
{
	DamageType "MonsterFire"
}
