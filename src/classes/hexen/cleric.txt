Actor ClericClass : ClericPlayer replaces ClericPlayer
{
	Player.StartItem "CWeapMace2"
	Player.StartItem "IsNotMonsterClass", 1
	Player.WeaponSlot 1, CWeapMace2
	Player.WeaponSlot 2, CWeapStaff2
	Player.WeaponSlot 3, CWeapFlame2
	Player.WeaponSlot 4, CWeapWraithverge2
	States
	{
		Death:
			CLER I 6 A_PlayerScream
			CLER JK 6
			CLER L 6 A_NoBlocking
			CLER MNOP 6
			CLER Q 6 A_SpawnItemEx("ReviveCleric")
			TNT1 A -1
			Stop
		XDeath:
			CLER R 5 A_PlayerScream
			CLER S 5 A_NoBlocking
			CLER TUVWXYZ 5
			CLER [ 5 A_SpawnItemEx("XReviveCleric")
			TNT1 A -1
			Stop

			Death.MonsterIce:
		XDeath.MonsterIce:
			CLER A 0 A_GiveToTarget("Health", 20)
			CLER A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Ice
		Death.MonsterFire:
		XDeath.MonsterFire:
			CLER A 0 A_GiveToTarget("Health", 20)
			CLER A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Burn
		Death.Monster:
			CLER A 0 A_GiveToTarget("Health", 20)
			CLER A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Death
		XDeath.Monster:
			CLER A 0 A_GiveToTarget("Health", 20)
			CLER A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto XDeath
	}
}

Actor CWeapMace2 : CWeapMace replaces CWeapMace
{
	Inventory.RestrictedTo "ClericClass"
	States
	{
		Select:
			CMCE A 1 A_Raise
			Loop
		Deselect:
			CMCE A 1 A_Lower
			Loop
		Ready:
			CMCE A 1 A_WeaponReady
			Loop
		Fire:
			CMCE B 2 Offset(60, 20)
			CMCE B 1 Offset(30, 33)
			CMCE B 2 Offset(8, 45)
			CMCE C 1 Offset(8, 45)
			CMCE D 1 Offset(8, 45)
			CMCE E 1 Offset(8, 45)
			CMCE E 1 Offset(-11, 58) A_CMaceAttack
			CMCE F 1 Offset(8, 45)
			CMCE F 2 Offset(-8, 74)
			CMCE F 1 Offset(-20, 96)
			CMCE F 8 Offset(-33, 160)
			CMCE A 2 Offset(8, 75) A_ReFire
			CMCE A 1 Offset(8, 65)
			CMCE A 2 Offset(8, 60)
			CMCE A 1 Offset(8, 55)
			CMCE A 2 Offset(8, 50)
			CMCE A 1 Offset(8, 45)
			Goto Ready
		AltFire:
			CMCE B 2 Offset(60, 20)
			CMCE B 1 Offset(30, 33)
			CMCE B 2 Offset(8, 45)
			CMCE C 1 Offset(8, 45)
			CMCE D 1 Offset(8, 45)
			CMCE E 1 Offset(8, 45)
			CMCE E 0 Offset(-11, 58) A_FireCustomMissile("ClericMaceMissile")
			CMCE E 0 Offset(-11, 58) A_FireCustomMissile("ClericMaceMissile", -6)
			CMCE E 0 Offset(-11, 58) A_FireCustomMissile("ClericMaceMissile", 6)
			CMCE E 1 Offset(-11, 58) A_CMaceAttack
			CMCE F 1 Offset(8, 45)
			CMCE F 2 Offset(-8, 74)
			CMCE F 1 Offset(-20, 96)
			CMCE F 8 Offset(-33, 160)
			CMCE A 2 Offset(8, 75)
			CMCE A 1 Offset(8, 65)
			CMCE A 2 Offset(8, 60)
			CMCE A 1 Offset(8, 55)
			CMCE A 2 Offset(8, 50)
			CMCE A 1 Offset(8, 45)
			Goto Ready
	}
}

Actor ClericMaceMissile : ProjectileBlade
{
	SeeSound "FireDemonAttack"
	Speed 50
	+RIPPER
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
}

Actor CWeapStaff2 : CWeapStaff replaces CWeapStaff
{
	Inventory.RestrictedTo "ClericClass"
	States
		{
			Spawn:
				WCSS A -1
				Stop
			Select:
				CSSF C 1 A_Raise
				Loop
			Deselect:
				CSSF B 3
				CSSF C 4
				CSSF C 1 A_Lower
				Wait
			Ready:
				CSSF C 4
				CSSF B 3 A_CStaffInitBlink
				CSSF AAAAAAA 1 A_WeaponReady
				CSSF A 1 A_CStaffCheckBlink
				Goto Ready+2
			Fire:
				CSSF A 0 Offset(0, 45) A_JumpIfInventory("Mana1", 1, 1)
				Goto Ready
				CSSF A 1 Offset(0, 45) A_CStaffCheck
				CSSF J 1 Offset(0, 50) A_CStaffAttack
				CSSF J 2 Offset(0, 50)
				CSSF J 2 Offset(0, 45)
				CSSF A 2 Offset(0, 40)
				CSSF A 2 Offset(0, 36)
				Goto Ready+2
			Blink:
				CSSF BBBCCCCCBBB 1 A_WeaponReady
				Goto Ready+2
			Drain:
				CSSF K 10 Offset(0, 36)
				Goto Ready+2
			AltFire:
				CSSF A 0 A_JumpIfCloser(96,"DrainMana")
				CSSF A 0 A_JumpIfInventory("Mana1",20,3)
				CSSF A 0 A_JumpIfInventory("Mana1",1,1)
				Goto Ready+2
				CSSF A 0 Offset(0, 45) A_TakeInventory("Mana1", 1)
				Goto Fire
				CSSF A 0 Offset(0, 45) A_TakeInventory("Mana1", 20)
				CSSF A 2 Offset(0, 45) 
				CSSF J 2 Offset(0, 50) A_FireCustomMissile("ClericStaffMissile")
				CSSF J 4 Offset(0, 50)
				CSSF J 4 Offset(0, 45)
				CSSF A 4 Offset(0, 40)
				CSSF A 4 Offset(0, 36)
				Goto Ready+2
			DrainMana:
				CSSF K 0 Offset (0,36) A_JumpIfInTargetInventory("Mana1", 5, 1)
				Goto AltFire+1
				CSSF K 0 Offset (0, 36) A_PlaySound("ClericCStaffHitThing")
				CSSF K 0 Offset (0, 36) A_TakeFromTarget("Mana1",5)
				CSSF K 0 Offset (0, 36) A_GiveInventory("Mana1",5)
				CSSF K 10 Offset (0, 36)
				Goto Ready+2
	  }
}

Actor ClericStaffMissile : CStaffMissile
{
	Speed 20
	Damage 6
	+RIPPER
	+CANNOTPUSH
	+NODAMAGETHRUST
	SeeSound "ClericCStaffFire"
	States
	{
		Spawn:
			D2FX ABCDEF 4 Bright
			loop
		Death:
			D2FX GHIJKL 4 Bright
			stop
	}
}

Actor CWeapFlame2 : CWeapFlame replaces CWeapFlame
{
	Inventory.RestrictedTo "ClericClass"
	States
	{
		Spawn:
			WCFM ABCDEFGH 4 Bright
			Loop
		Select:
			CFLM AAAABBBBCCCC 1 A_Raise
			Loop
		Deselect:
			CFLM AAAABBBBCCCC 1 A_Lower
			Loop
		Ready:
			CFLM AAAABBBBCCCC 1 A_WeaponReady
			Loop
		Fire:
			CFLM A 2 Offset(0, 40)
			CFLM D 2 Offset(0, 50)
			CFLM D 2 Offset(0, 36)
			CFLM E 4 Bright
			CFLM F 4 Bright A_CFlameAttack
			CFLM E 4 Bright
			CFLM G 2 Offset(0, 40)
			CFLM G 2
			Goto Ready
		AltFire:
			CFLM A 2 Offset(0, 40)
			CFLM D 2 Offset(0, 50)
			CFLM D 2 Offset(0, 36)
			CFLM B 0 A_JumpIfInventory("Mana2", 1,1)
			Goto End
		AltHold:
			CFLM B 0 A_JumpIfInventory("Mana2", 1,1)
			Goto End
			CFLM F 1 Bright A_FireCustomMissile("CWeapFlameThrower")
			CFLM E 0 Bright A_TakeInventory("Mana2", 1)
			CFLM B 0 A_JumpIfInventory("Mana2", 1,1)
			Goto Ready
			CFLM E 0 Bright A_ReFire
		End:
			CFLM G 2 Offset(0, 40)
			CFLM G 2
			Goto Ready
	}
}

Actor CWeapFlameThrower
{
	SeeSound "ClericFlameFire"
	Projectile
	Speed 20
	Damage 1
	Radius 8
	Height 16
	+RIPPER
	DamageType "Fire"
	RenderStyle "Add"
	States
	{
		Spawn:
			CFFX ABCDEFGHIJKLM 1 Bright
		Death:
			CFFX KLM 1 Bright
			stop
	}
}

Actor CWeaponPiece1_2 : CWeaponPiece1 replaces CWeaponPiece1
{
	Inventory.RestrictedTo "ClericClass"
	WeaponPiece.Weapon CWeapWraithverge2
}

Actor CWeaponPiece2_2 : CWeaponPiece2 replaces CWeaponPiece2
{
	Inventory.RestrictedTo "ClericClass"
	WeaponPiece.Weapon CWeapWraithverge2
}

Actor CWeaponPiece3_2 : CWeaponPiece3 replaces CWeaponPiece3
{
	Inventory.RestrictedTo "ClericClass"
	WeaponPiece.Weapon CWeapWraithverge2
}

Actor CWeapWraithverge2 : CWeapWraithverge replaces CWeapWraithverge
{
	Inventory.RestrictedTo "ClericClass"
	States
	{
		Spawn:
			TNT1 A -1
			Stop
		Ready:
			CHLY A 1 A_WeaponReady
			Loop
		Select:
			CHLY A 1 A_Raise
			Loop
		Deselect:
			CHLY A 1 A_Lower
			Loop
		Fire:
			CHLY AB 1 Bright Offset(0, 40)
			CHLY CD 2 Bright Offset(0, 43)
			CHLY E 2 Bright Offset(0, 45)
			CHLY F 6 Bright Offset(0, 48) A_CHolyAttack
			CHLY GG 2 Bright Offset(0, 40) A_CHolyPalette
			CHLY G 2 Offset(0, 36) A_CHolyPalette
			Goto Ready
		AltFire:
			CHLY D 0 A_JumpIfInventory("Mana1", 30, "Test2")
			CHLY D 0 A_JumpIfInventory("Mana1", 18, 1)
			Goto Ready
			CHLY D 0 A_JumpIfInventory("Mana2", 18, 1)
			Goto Ready
			CHLY D 0
			CHLY D 0 A_TakeInventory("Mana1", 18)
			Goto Fire
		Test2:
			CHLY D 0 A_JumpIfInventory("Mana2", 30, "AltGood")
			CHLY D 0 A_JumpIfInventory("Mana2", 18, 1)
			Goto Ready
			CHLY D 0 A_TakeInventory("Mana1", 18)
			Goto Fire
		AltGood:
			CHLY AB 1 Bright Offset(0, 40)
			CHLY CD 2 Bright Offset(0, 43)
			CHLY E 2 Bright Offset(0, 45)
			MSTF D 0 A_TakeInventory("Mana1", 30)
			MSTF D 0 A_TakeInventory("Mana2", 12)
			CHLY F 6 Bright Offset(0, 48) A_FireCustomMissile("ClericMissile")
			CHLY GG 2 Bright Offset(0, 40) A_CHolyPalette
			CHLY G 2 Offset(0, 36) A_CHolyPalette
			Goto Ready
	}
}

Actor ClericMissile
{
	Projectile
	Radius 8
	Height 16
	Damage 50
	Speed 40
	+RIPPER
	SeeSound "HolySymbolFire"
	DeathSound "SpiritDie"
	States
	{
		Spawn:
			ETHS AB 8 Bright
			loop
		Death:
			ETHS CDEFGHIJ 4 Bright
			stop
	}
}