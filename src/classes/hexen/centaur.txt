Actor CentaurMonsterClass : MonsterClass
{
	Player.StartItem "IsHexenMonster"
	Player.ColorRange 97, 121
	Player.ViewHeight 56
	Player.DisplayName "Centaur"
	Player.JumpZ 10
	PainSound "CentaurPain"
	Player.SoundClass "centaur"
	Player.StartItem "CentaurWeapon"
	Player.WeaponSlot 1, CentaurWeapon
	Player.StartItem "MonsterHealth", 50
	Player.ForwardMove 0.8, 0.8
	Player.SideMove 0.8, 0.8
	HowlSound "PuppyBeat"
	Player.Face "CNT"
	
	+SHIELDREFLECT
	States
	{
		Spawn:
			CENT A -1
			stop
		See:
			CENT ABCD 4
			loop
		Pain:
			CENT G 8 A_Pain
			Goto Spawn
		Melee:
			CENT A 1
			CENT A 1 A_JumpIfInventory("CentBlock",1,"Block")
			CENT E 10
			CENT F 8 Bright
			CENT E 10
			Goto Spawn
		Missile:
			CENT A 1
			CENT A 1 A_JumpIfInventory("CentBlock",1,"Block")
			CENT H 5
			CENT I 4
			CENT J 7
			Goto Spawn
		Block:
			CENT E 0 A_TakeInventory("CentBlock",1)
			CENT E 70
			Goto Spawn
		Death:
			CENT K 4 A_Scream
			CENT L 4 A_NoBlocking
			CENT MNOPQRS 4
			CENT T 0 A_Jump(63, "SpawnLeader")
			CENT T 4 A_SpawnItemEx("ReviveCentaur")
			TNT1 A -1
			stop
		SpawnLeader:
			CENT T 4 A_SpawnItemEx("ReviveCentaurLeader")
			TNT1 A -1
			stop
		XDeath:
			CTXD A 4 A_Scream
			CTXD B 4 A_NoBlocking
			CTXD CDEFGHIJ 4
			CTXD K 0 A_Jump(63, "XSpawnLeader")
			CTXD K 4 A_SpawnItemEx("XReviveCentaur")
			TNT1 A -1
			stop
		SpawnLeader:
			CENT T 4 A_SpawnItemEx("XReviveCentaurLeader")
			TNT1 A -1
			stop
		Ice:
			CENT U 5 A_FreezeDeath
			CENT U 1 A_FreezeDeathChunks
			wait

		Death.MonsterIce:
		XDeath.MonsterIce:
			CENT A 0 A_GiveToTarget("Health", 25)
			CENT A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Ice
		Death.Monster:
		Death.MonsterFire:
			CENT A 0 A_GiveToTarget("Health", 25)
			CENT A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Death
		XDeath.Monster:
		XDeath.MonsterFire:
			CENT A 0 A_GiveToTarget("Health", 25)
			CENT A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto XDeath
	}	
}

Actor CentaurWeapon : Weapon
{
	Inventory.RestrictedTo "CentaurMonsterClass"
	States
	{
		Spawn:
			TNT1 A 1
			stop
		Select:
			CSWD A 1 A_Raise
			loop
		Deselect:
			CSWD A 1 A_Lower
			loop
		Ready:
			CSWD A 1 A_WeaponReady
			loop
		Fire:
			CSWD B 0 A_JumpIfCloser(106,"Melee")
			CSWD G 12 A_GunFlash
			CSWD G 8 Bright A_FireCustomMissile("CentaurFX_2")
			CSWD G 10
			Goto Ready
		Melee:
			CSWD BCD 3
			CSWD E 3 A_CustomPunch(10,0,0,"CentaurPuff",106)
			CSWD F 5
			CSWD F 0 A_ReFire
			CSWD A 4 Offset(0,60)
			CSWD A 4 Offset(0,55)
			CSWD A 4 Offset(0,50)
			CSWD A 4 Offset(0,45)
			Goto Ready
		AltFire:
			CSWD G 0 A_JumpIfInventory("ShieldCoolDown", 1, "Fire")
			CSWD G 0 A_GiveInventory("CentBlock",1)
			CSWD G 0 A_SetReflectiveInvulnerable
			CSWD GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 A_Stop
			CSWD G 0 A_UnsetReflectiveInvulnerable
			CSWD G 0 A_GiveInventory("ShieldCoolDown", 1)
			CSWD G 0 ACS_ExecuteAlways(998)
			Goto Ready
	}
}

Actor ShieldCoolDown : Inventory
{
	Inventory.MaxAmount 1
}

Actor CentaurFX_2 : CentaurFX
{
	Damage 8
	DamageType "Monster"
}

Actor CentBlock : Inventory
{
	Inventory.MaxAmount 1
}

Actor CentaurPuff : HammerPuff
{
	SeeSound "CentaurAttack"
	DamageType "Monster"
}
