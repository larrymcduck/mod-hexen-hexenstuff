Actor DemonMonsterClass : MonsterClass
{
	Height 90
	Radius 23

	Player.StartItem "IsHexenMonster"
	Player.Viewheight 72
	Player.DisplayName "Chaos_Serpent"
	Player.ColorRange 97, 107
	Player.JumpZ 8
	PainSound "DemonPain"
	Player.SoundClass "demon"
	Player.StartItem "MonsterHealth", 50
	Player.ForwardMove 0.8, 0.8
	Player.SideMove 0.8, 0.8
	Player.StartItem "DemonWeapon"
	Player.WeaponSlot 1, DemonWeapon
	Player.StartItem "MonsterPowerAttack"
	Player.Face "DMN"
	
	States
	{
		Spawn:
			DEM2 A -1
			loop
		See:
			DEM2 ABCD 4
			loop
		Pain:
			DEM2 E 8 A_Pain
			Goto See
		Missile:
		Melee:
			DEM2 EFG 6
			Goto Spawn
		XDeath:
		Death:
			DEM2 H 4
			DEM2 I 4 A_PlayerScream
			DEM2 J 4 A_NoBlocking
			DEM2 KLMNO 4
			DEM2 P 0 A_Jump(127, "SpawnBrown")
			DEM2 P 4 A_SpawnItemEx("ReviveDemon1")
			TNT1 A -1
			stop
		SpawnBrown:
			DEM2 P 4 A_SpawnItemEx("ReviveDemon2")
			TNT1 A -1
			stop
		Ice:
			DEM2 Q 5 A_FreezeDeath
			DEM2 Q 1 A_FreezeDeathChunks
			wait

		Death.MonsterIce:
		XDeath.MonsterIce:
			DEM2 A 0 A_GiveToTarget("Health", 25)
			DEM2 A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Ice
		Death.Monster:
		Death.MonsterFire:
			DEM2 A 0 A_GiveToTarget("Health", 25)
			DEM2 A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Death
		XDeath.Monster:
		XDeath.MonsterFire:
			DEM2 A 0 A_GiveToTarget("Health", 25)
			DEM2 A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto XDeath
	}
}

Actor DemonWeapon : Weapon
{
	Inventory.RestrictedTo "DemonMonsterClass"
	DamageType "Monster"
	States
	{
		Spawn:
			TNT1 A 1
			stop
		Select:
			TNT1 A 1 A_Raise
			loop
		Deselect:
			TNT1 A 1 A_Lower
			loop
		Ready:
			TNT1 A 1 A_WeaponReady
			loop
		Fire:
			TNT1 A 17 A_CustomPunch(12,0,0,"DemonMonsterPuff",96)
			Goto Ready
		PoisonBall:
			TNT1 A 1 A_FireCustomMissile("Demon2FX1_2",0,0,0,16)
		WaitFire:
			TNT1 A 17
			Goto Ready
			
		AltFire:
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 10, "Level4")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 6, "Level3")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 3, "Level2")
			TNT1 A 0 A_Jump(40, "PoisonBall")
			TNT1 A 1 A_FireCustomMissile("Demon1FX1_2",0,0,0,16)
			Goto WaitFire
		Level2:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 3)
			TNT1 A 1 A_FireCustomMissile("DragonFireball_2",0,0,0,16)
			Goto WaitFire
		Level3:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 10)
			TNT1 A 0 A_FireCustomMissile("HereFireBounce", -20)
			TNT1 A 1 A_FireCustomMissile("HereFireBounce", 20)
			Goto WaitFire
		Level4:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 10)
			TNT1 A 0 A_PlaySound("MageStaffFire")
			TNT1 A 0 A_FireCustomMissile("MageStaffFX2", -5,0,0,16)
			TNT1 A 0 A_FireCustomMissile("MageStaffFX2",0,0,0,16)
			TNT1 A 5 A_FireCustomMissile("MageStaffFX2", 5,0,0,16)
			Goto WaitFire
	}
}

Actor Demon1FX1_2 : Demon1FX1
{
	Damage 8
	DamageType "MonsterFire"
}

Actor Demon2FX1_2 : Demon2FX1
{
	PoisonDamage 10
	DamageType "Monster"
}

Actor DemonMonsterPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	DamageType "Monster"
	SeeSound "DemonAttack"
	AttackSound "DemonAttack"
	ActiveSound "DemonAttack"
	States
	{
		Spawn:
			TNT1 A 24
			stop
	}
}
