Actor MageClass : MagePlayer replaces MagePlayer
{
	Player.StartItem "MWeapWand2"
	Player.StartItem "IsNotMonsterClass", 1
	Player.WeaponSlot 1, MWeapWand2
	Player.WeaponSlot 2, MWeapFrost2
	Player.WeaponSlot 3, MWeapLightning2
	Player.WeaponSlot 4, MWeapBloodscourge2
	States
	{
		Death:
			MAGE H 6
			MAGE I 6 A_Scream
			MAGE JK 6
			MAGE L 6 A_NoBlocking
			MAGE M 6
			MAGE N 6 A_SpawnItemEx("ReviveMage")
			TNT1 A -1
			Stop
		XDeath:
			MAGE O 5 A_Scream
			MAGE P 5 A_NoBlocking
			MAGE QRSTUVW 5
			MAGE X 5 A_SpawnItemEx("XReviveMage")
			TNT1 A -1
			Stop			

		Death.MonsterIce:
		XDeath.MonsterIce:
			MAGE A 0 A_GiveToTarget("Health", 20)
			MAGE A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Ice
		Death.MonsterFire:
		XDeath.MonsterFire:
			MAGE A 0 A_GiveToTarget("Health", 20)
			MAGE A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Burn
		Death.Monster:
			MAGE A 0 A_GiveToTarget("Health", 20)
			MAGE A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Death
		XDeath.Monster:
			MAGE A 0 A_GiveToTarget("Health", 20)
			MAGE A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto XDeath
	}
}

Actor MWeapWand2 : MWeapWand replaces MWeapWand
{
	Inventory.RestrictedTo "MageClass"
	States
	{
		Select:
			MWND A 1 A_Raise
			Loop
		Deselect:
			MWND A 1 A_Lower
			Loop
		Ready:
			MWND A 1 A_WeaponReady
			Loop
		Fire:
			MWND A 6
			MWND B 6 Bright Offset(0, 48) A_FireCustomMissile("MageWandMissile")
			MWND A 3 Offset(0, 40)
			MWND A 3 Offset(0, 36) A_ReFire
			Goto Ready
		AltFire:
			MWND J 12
			MWND K 12 Bright Offset(0, 48) A_FireCustomMissile("MageWandMissile2")
			MWND J 6 Offset(0, 40)
			MWND J 6 Offset(0, 36) A_ReFire
			Goto Ready
	}
}

Actor MageWandMissile2
{
	SeeSound "MageWandFire"
	Projectile
	+RIPPER
	Speed 20
	Radius 4
	Height 8
	Damage 5
	States
	{
		Spawn:
			TRNG E 1 Bright
			TRNG E 0 A_SpawnItem("MageWandSmoke2")
			loop
		Death:
			TRNG DCBA 4 Bright
			stop
	}
}

Actor MageWandSmoke2 : MageWandSmoke
{
	States
	{
		Spawn:
			TRNG DCVAVCDE 1
			stop
	}
}

Actor MWeapFrost2 : MWeapFrost replaces MWeapFrost
{
	Inventory.RestrictedTo "MageClass"
	States
	{
		Spawn:
			WMCS ABC 8 Bright
			Loop
		Select:
			CONE A 1 A_Raise
			Loop
		Deselect:
			CONE A 1 A_Lower
			Loop
		Ready:
			CONE A 1 A_WeaponReady
			Loop
		Fire:
			CONE B 3
			CONE C 4
		Hold:
			CONE D 3
			CONE E 5
			CONE F 3 A_FireConePL1
			CONE G 3
			CONE A 9
			CONE A 10 A_ReFire
			Goto Ready
		AltFire:
			CONE C 0 A_JumpIfInventory("Mana1", 3, 1)
			Goto Ready
			CONE C 0 A_JumpIfInventory("Mana1", 20, 3)
			CONE C 0 A_JumpIfInventory("Mana1", 3, 1)
			Goto Ready
			CONE C 0 A_TakeInventory("Mana1", 3)
			Goto Fire
		AltHold:
			CONE B 3
			CONE C 4
			CONE C 0 A_JumpIfInventory("Mana1", 20, 1)
			Goto Ready
			CONE C 0 A_TakeInventory("Mana1", 20)
			CONE D 3
			CONE E 5
			CONE F 3 A_FireCustomMissile("MageCone2")
			CONE G 3
			CONE A 9
			CONE A 10 A_ReFire
			Goto Ready
	}
}

Actor MageCone2 : HornRodFX2
{
	DamageType "Ice"
	SeeSound "IceGuyAttack"
	DeathSound "MageShardsExplode"
	States
	{
		Spawn:
			ICPR ABC 3 Bright A_SeekerMissile(10,30)
			loop
		Death:
			ICPR D 5 Bright A_AddPlayerRain
			ICPR E 5 Bright
			ICPR F 5 Bright
			ICPR G 5 Bright
			ICPR H 5 Bright
			TNT1 H 1 Bright A_HideInCeiling
			TNT1 H 1 Bright A_SkullRodStorm
			wait
	}
}

Actor RainPillar2 : RainPillar replaces RainPillar
{
	Speed 20
	+RIPPER
	+CANNOTPUSH
	+NODAMAGETHRUST
	DamageType "Ice"
	DeathSound "IceGuyAttack"
	States
	{
		Spawn:
			ICT3 A -1 Bright
		Death:
			ICT3 A 3 Bright A_RainImpact
			stop
		NotFloor:
			ICT3 AAA 4 Bright
			Stop
	}
}

Actor MWeapLightning2 : MWeapLightning replaces MWeapLightning
{
	Inventory.RestrictedTo "MageClass"
	States
	{
		Spawn:
			WMLG ABCDEFGH 4 Bright
			Loop
		Select:
			MLNG A 1 Bright A_Raise
			Loop
		Deselect:
			MLNG A 1 Bright A_Lower
			Loop
		Ready:
			MLNG AAAAA 1 Bright A_WeaponReady
			MLNG A 1 Bright A_LightningReady
			MLNG BBBBBB 1 Bright A_WeaponReady
			MLNG CCCCC 1 Bright A_WeaponReady
			MLNG C 1 Bright A_LightningReady
			MLNG BBBBBB 1 Bright A_WeaponReady
			Loop
		Fire:
			MLNG DE 3 Bright
			MLNG F 4 Bright A_MLightningAttack
			MLNG G 4 Bright
			MLNG HI 3 Bright
			MLNG I 6 Bright Offset(0, 199)
			MLNG C 2 Bright Offset(0, 55)
			MLNG B 2 Bright Offset(0, 50)
			MLNG B 2 Bright Offset(0, 45)
			MLNG B 2 Bright Offset(0, 40)
			Goto Ready
		AltFire:
			MLNG D 0 A_JumpIfInventory("Mana2", 30, 3)
			MLNG D 0 A_JumpIfInventory("Mana2", 5, 1)
			Goto Ready
			MLNG D 0 A_TakeInventory("Mana2", 5)
			Goto Fire			
			MLNG DE 3 Bright
			MLNG D 0 A_TakeInventory("Mana2", 30)
			MLNG F 4 Bright A_FireCustomMissile("MLightingSend")
			MLNG G 4 Bright
			MLNG HI 3 Bright
			MLNG I 6 Bright Offset(0, 199)
			MLNG C 2 Bright Offset(0, 55)
			MLNG B 2 Bright Offset(0, 50)
			MLNG B 2 Bright Offset(0, 45)
			MLNG B 2 Bright Offset(0, 40)
			Goto Ready
	}
}

Actor MLightingSend : HornRodFX2
{
	Speed 50
	Radius 4
	Height 8
	Damage 10
	SeeSound "MageLightningFire"
	+SPAWNSOUNDSOURCE
	States
	{
		Spawn:
			MLF2 PQPQ 3
		Death:
			TNT1 A 1 A_HideInCeiling
			TNT1 A 0 A_PlaySound("MageLightningStrike")
			TNT1 A 1 A_SpawnItemEx("MLightning", 8, 0, 0, 0, 0, -64)
			TNT1 A 1 A_SpawnItemEx("MLightning", 8, 0, 0, 0, 0, -64)
			TNT1 A 1 A_SpawnItemEx("MLightning", 8, 0, 0, 0, 0, -64)
			TNT1 A 1 A_SpawnItemEx("MLightning", 8, 0, 0, 0, 0, -64)
			TNT1 A 1 A_SpawnItemEx("MLightning", 8, 0, 0, 0, 0, -64)
			TNT1 A 1 A_SpawnItemEx("MLightning", 8, 0, 0, 0, 0, -64)
			TNT1 A 1 A_SpawnItemEx("MLightning", 8, 0, 0, 0, 0, -64)
			TNT1 A 1 A_SpawnItemEx("MLightning", 8, 0, 0, 0, 0, -64)
			TNT1 A 1 A_SpawnItemEx("MLightning", 8, 0, 0, 0, 0, -64)
			TNT1 A 1 A_SpawnItemEx("MLightning", 8, 0, 0, 0, 0, -64)
			stop
	}
}

Actor MLightning
{
	Projectile
	Damage 10
	Height 1
	Radius 8
	+RIPPER
	States
	{	
		Spawn:
			MLFX IJLM 3 Bright
			loop
		Death:
			TNT1 A 1
			stop
	}
}

Actor MWeaponPiece1_2 : MWeaponPiece1 replaces MWeaponPiece1
{
	Inventory.RestrictedTo "MageClass"
	WeaponPiece.Weapon MWeapBloodscourge2
}

Actor MWeaponPiece2_2 : MWeaponPiece2 replaces MWeaponPiece2
{
	Inventory.RestrictedTo "MageClass"
	WeaponPiece.Weapon MWeapBloodscourge2
}

Actor MWeaponPiece3_2 : MWeaponPiece3 replaces MWeaponPiece3
{
	Inventory.RestrictedTo "MageClass"
	WeaponPiece.Weapon MWeapBloodscourge2
}

Actor MWeapBloodscourge2 : MWeapBloodscourge replaces MWeapBloodscourge
{
	Inventory.RestrictedTo "MageClass"
	States
	{
		Spawn:
			TNT1 A -1
			Stop
		Select:
			MSTF A 1 A_Raise
			Loop
		Deselect:
			MSTF A 1 A_Lower
			Loop
		Ready:
			MSTF AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFF 1 A_WeaponReady
			Loop
		Fire:
			MSTF G 4 Offset(0, 40)
			MSTF H 4 Bright Offset(0, 48) A_MStaffAttack
			MSTF H 2 Bright Offset(0, 48) A_MStaffPalette
			MSTF II 2 Offset(0, 48) A_MStaffPalette
			MSTF I 1 Offset(0, 40)
			MSTF J 5 Offset(0, 36)
			Goto Ready
		AltFire:
			MSTF D 0 A_JumpIfInventory("Mana1", 50, "Test2")
			MSTF D 0 A_JumpIfInventory("Mana1", 15, 1)
			Goto Ready
			MSTF D 0 A_JumpIfInventory("Mana2", 15, 1)
			Goto Ready
			MSTF D 0 A_TakeInventory("Mana1", 15)
			Goto Fire
		Test2:
			MSTF D 0 A_JumpIfInventory("Mana2", 50, "AltGood")
			MSTF D 0 A_JumpIfInventory("Mana2", 15, 1)
			Goto Ready
			MSTF D 0 A_TakeInventory("Mana1", 15)
			Goto Fire
		AltGood:
			MSTF G 4 Offset(0, 40)
			MSTF D 0 A_TakeInventory("Mana1", 50)
			MSTF D 0 A_TakeInventory("Mana2", 35)
			MSTF H 4 Bright Offset(0, 48) A_FireCustomMissile("MageStaffMissile2")
			MSTF H 2 Bright Offset(0, 48) A_MStaffPalette
			MSTF II 2 Offset(0, 48) A_MStaffPalette
			MSTF I 1 Offset(0, 40)
			MSTF J 5 Offset(0, 36)
			Goto Ready
	}
}

Actor MageStaffMissile2
{
	Radius 8
	Height 16
	Projectile
	+RIPPER
	Speed 25
	Damage 100
	SeeSound "MageStaffFire"
	DeathSound "MageStaffExplode"
	States
	{
		Spawn:
			SBS1 ABCD 4 Bright
			loop
		Death:
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 0)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 22.5)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 45)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 67.5)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 90)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 112.5)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 135)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 157.5)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 180)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 202.5)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 225)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 247.5)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 270)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 292.5)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 315)
			TNT1 A 0 A_SpawnItemEx("MageStaffMissile3", 0, 0, 0, 20, 0, 0, 337.5)
			MSP1 GHIJKLMNOP 3 Bright
			stop
	}
}

Actor MageStaffMissile3
{
	Radius 4
	Height 8
	Projectile
	+RIPPER
	Damage 3
	SeeSound "MageStaffFire"
	DeathSound "MageStaffExplode"
	States
	{
		Spawn:
			WRBL ABC 4 Bright
			loop
		Death:
			WRBL DEFGHI 4 Bright
			stop
	}
}