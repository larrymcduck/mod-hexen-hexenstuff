Actor FighterMonsterClass : FighterPlayer
{
	Player.StartItem "IsHexenMonster"
	Player.StartItem "FWeapQuietus3"
	Player.WeaponSlot 1, FWeapQuietus3
	Player.DisplayName "Zedek"
	Player.StartItem "MonsterHealth", 100
	Player.StartItem "MonsterPowerAttack"
	Player.Face "ZDK"
	-PICKUP
	
	Player.ColorRange 97, 121
	
	States
	{
		Death:
			PLAY H 6
			PLAY I 6 A_PlayerScream
			PLAY JK 6
			PLAY L 6 A_NoBlocking
			PLAY M 6
			PLAY N 6 A_SpawnItemEx("ReviveFighter")
			TNT1 A -1
			Stop
		XDeath:
			PLAY O 5 A_PlayerScream
			PLAY P 5 A_SkullPop("BloodyFighterSkull")
			PLAY R 5 A_NoBlocking
			PLAY STUV 5
			PLAY W 5 A_SpawnItemEx("XReviveFighter")
			TNT1 A -1
			Stop
			
		Death.MonsterIce:
		XDeath.MonsterIce:
			PLAY A 0 A_GiveToTarget("Health", 25)
			PLAY A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Ice
		Death.Monster:
			PLAY A 0 A_GiveToTarget("Health", 25)
			PLAY A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Death
		XDeath.Monster:
			PLAY A 0 A_GiveToTarget("Health", 25)
			PLAY A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto XDeath
		Death.MonsterFire:
		XDeath.MonsterFire:
			PLAY A 0 A_GiveToTarget("Health", 25)
			PLAY A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Burn
	}
}

Actor FWeapQuietus3 : FWeapQuietus
{
	Inventory.RestrictedTo "FighterMonsterClass"
	Weapon.AmmoUse1 0
	Weapon.AmmoUse2 0
	States
	{
		Spawn:
			TNT1 A 1
			Stop
		Fire:
			FSRD DE 3 Bright Offset(5, 36)
			FSRD F 2 Bright Offset(5, 36)
			FSRD G 0 A_PlaySound("FighterSwordFire", 1)
			FSRD G 0 A_FireCustomMissile("FSwordMissile_2", -11.5)
			FSRD G 0 A_FireCustomMissile("FSwordMissile_2")
			FSRD G 3 Bright Offset(5, 36) A_FireCustomMissile("FSwordMissile_2", 11.5)
		WaitFire:
			FSRD H 2 Bright Offset(5, 36)
			FSRD I 2 Bright Offset(5, 36)
			FSRD I 10 Bright Offset(5, 150)
			FSRD I 70
			FSRD A 1 Bright Offset(5, 60)
			FSRD B 1 Bright Offset(5, 55)
			FSRD C 1 Bright Offset(5, 50)
			FSRD A 1 Bright Offset(5, 45)
			FSRD B 1 Bright Offset(5, 40)
			Goto Ready
		AltFire:
			FSRD DE 3 Bright Offset(5, 36)
			FSRD F 2 Bright Offset(5, 36)
			FSRD G 0 A_PlaySound("FighterSwordFire", 1)
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 10, "Level4")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 6, "Level3")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 3, "Level2")
			Goto Fire+4
		Level2:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 3)
			FSRD G 0 A_FireCustomMissile("FSwordMissile_2", -11.5)
			FSRD G 0 A_FireCustomMissile("FSwordMissile_2", -5.625)
			FSRD G 0 A_FireCustomMissile("FSwordMissile_2")
			FSRD G 0 A_FireCustomMissile("FSwordMissile_2", 5.625)
			FSRD G 3 Bright Offset(5, 36) A_FireCustomMissile("FSwordMissile_2", 11.5)
			Goto WaitFire
		Level3:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 6)
			FSRD G 0 A_FireCustomMissile("FSwordMissile_3", -11.5)
			FSRD G 0 A_FireCustomMissile("FSwordMissile_3")
			FSRD G 3 Bright Offset(5, 36) A_FireCustomMissile("FSwordMissile_3", 11.5)
			Goto WaitFire
		Level4:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 10)
			FSRD G 0 A_FireCustomMissile("FSwordMissile_3", -11.5)
			FSRD G 0 A_FireCustomMissile("FSwordMissile_3", -5.625)
			FSRD G 0 A_FireCustomMissile("FSwordMissile_3")
			FSRD G 0 A_FireCustomMissile("FSwordMissile_3", 5.625)
			FSRD G 3 Bright Offset(5, 36) A_FireCustomMissile("FSwordMissile_3", 11.5)
			Goto WaitFire
	}
}

Actor FSwordMissile_2 : FSwordMissile
{
	DamageType "Monster"
}

Actor FSwordMissile_3 : FSwordMissile_2
{
	Damage 16
	Translation "186:202=164:185","211:216=164:185"
	States
	{
		Death:
			FSFX D 4 Bright
			FSFX E 3 Bright
			FSFX F 4 Bright A_Explode(128, 128, 0)
			FSFX G 3 Bright
			FSFX H 4 Bright
			FSFX I 3 Bright
			FSFX J 4 Bright
			FSFX KLM 3 Bright
			Stop
	}
}