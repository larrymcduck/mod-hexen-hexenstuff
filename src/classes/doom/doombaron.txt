Actor DoomBaronMonsterClass : MonsterClass
{
	Player.StartItem "IsDoomMonster"
	Player.DisplayName "Baron_of_Hell"
	Player.StartItem "MonsterHealth", 150
	Player.ForwardMove 0.6, 0.6
	Player.SideMove 0.6, 0.6
	Player.Hexenarmor 50, 35, 30, 25, 15
	Player.JumpZ 9
	PainSound "doombaron/pain"
	Player.SoundClass "doombaron"
	Player.StartItem "DoomBaronWeapon"
	Player.StartItem "MonsterPowerAttack"
	Player.WeaponSlot 1, DoomBaronWeapon
	Player.ColorRange 81, 121
	Player.Face "BOH"
	
	States
	{
		Spawn:
			BOSS A -1
			stop
		See:
			BOSS ABCD 6
			loop
		Melee:
		Missile:
			BOSS EFG 8
			Goto Spawn
		Pain:
			BOSS H 8 A_Pain
			Goto Spawn
		Death:
			BOSS I 8 A_PlayerScream
			BOSS J 8 A_NoBlocking
			BOSS KLMN 8
			BOSS O 8 A_SpawnItemEx("ReviveBaron")
			TNT1 A -1
			stop
			
		Death.MonsterIce:
		XDeath.MonsterIce:
			FDMN A 0 A_GiveToTarget("Health", 50)
			FDMN A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Ice
		Death.Monster:
		Death.MonsterFire:
		XDeath.MonsterFire:
		XDeath.Monster:
			FDMN A 0 A_GiveToTarget("Health", 50)
			FDMN A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Death
	}
}

Actor DoomBaronWeapon : Weapon
{
	Inventory.RestrictedTo "DoomBaronMonsterClass"
	States
	{
		Spawn:
			TNT1 A 1
			stop
		Select:
			BHND A 1 A_Raise
			Goto Ready
		Ready:
			BHND AAAEEEFFF 1 A_WeaponReady
			Loop
		DeSelect:
			BHND A 1 A_Lower
			Loop
		Fire:
			BHND BC 6
			BHND D 6 A_CustomPunch (10,0,1,"DoomImpMonsterPuff")
		WaitFire:
			BHND CB 4
			Goto Ready

		AltFire:
			BHND BC 6
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 10, "Level4")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 6, "Level3")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 3, "Level2")
			BHND D 6 A_FireCustomMissile("BaronBall_2")
			Goto WaitFire
		Level2:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 3)
			BHND D 0 A_FireCustomMissile("BaronBall_2", -4)
			BHND D 6 A_FireCustomMissile("BaronBall_2", 4)
			Goto WaitFire
		Level3:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 6)
			BHND D 0 A_FireCustomMissile("BaronBall_2", -6)
			BHND D 0 A_FireCustomMissile("BaronBall_2", 0)
			BHND D 6 A_FireCustomMissile("BaronBall_2", 6)
			Goto WaitFire
		Level4:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 10)
			BHND D 0 A_FireCustomMissile("BaronBall_3", -6)
			BHND D 0 A_FireCustomMissile("BaronBall_3", 0)
			BHND D 6 A_FireCustomMissile("BaronBall_3", 6)
			Goto WaitFire
	}
}

Actor BaronBall_2 : BaronBall replaces BaronBall
{
	DamageType "Monster"
	Damage 20
	SeeSound "doombaron/attack"
	DeathSound "doombaron/shotx"
}

Actor BaronBall_3 : BaronBall_2
{
	+RIPPER
	RenderStyle Normal

	Damage 8
	Speed 50
	
	States
	{
		Spawn:
			FSFX ABC 4 Bright
			Loop
		Death:
			FSFX D 4 Bright A_Explode(64, 128, 0)
			FSFX EFGHIJKLM 4 Bright
			Stop
	}
}