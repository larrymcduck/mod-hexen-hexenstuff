Actor DoomCacodemonMonsterClass : MonsterClass
{	
	Player.StartItem "IsDoomMonster"
	Player.DisplayName "Cacodemon"
	Player.StartItem "MonsterHealth", 75
	Player.StartItem "MonsterFlight"
	Player.ForwardMove 0.6, 0.6
	Player.SideMove 0.6, 0.6
	PainSound "doomcacodemon/pain"
	Player.SoundClass "doomcacodemon"
	Player.StartItem "DoomCacodemonWeapon"
	Player.StartItem "MonsterPowerAttack"
	Player.WeaponSlot 1, DoomCacodemonWeapon
	Player.ColorRange 164, 185
	Player.Face "CDM"
	
	States
	{
		Spawn:
			CDMN AA 4 A_GiveInventory("MonsterFlight")
			Loop
		See:
			CDMN A 4
			loop
		Melee:
		Missile:
			CDMN BCD 5
			Goto Spawn
		Pain:
			CDMN FE 4 A_Pain
			Goto Spawn

		Death.Monster:
		Death.MonsterFire:
		XDeath.MonsterFire:
		XDeath.Monster:
			FDMN A 0 A_GiveToTarget("Health", 25)
			FDMN A 0 A_GiveToTarget("DiscipleSummons", 1)
		Death:
			CDMN G 8 A_PlayerScream
			CDMN H 8 A_NoBlocking
			CDMN IJK 8
			CDMN L 8 A_SpawnItemEx("ReviveCacodemon")
			TNT1 A -1
			stop
			
		Death.MonsterIce:
		XDeath.MonsterIce:
			FDMN A 0 A_GiveToTarget("Health", 25)
			FDMN A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Ice
	}
}

Actor DoomCacodemonWeapon : Weapon
{
	Inventory.RestrictedTo "DoomCacodemonMonsterClass"
	+WEAPON.DONTBOB
	
	States
	{
		Spawn:
			TNT1 A 1
			stop
		Select:
			CACO A 1 A_Raise
			Goto Ready
		Ready:
			CACO A 1 A_WeaponReady
			Loop
		DeSelect:
			CACO A 1 A_Lower
			Loop
		Fire:
			CACO A 1 Offset(0,34)
			CACO A 1 Offset(0,36)
			CACO A 1 Offset(0,38)
			CACO A 1 Offset(0,40)
			CACO A 1 Offset(0,44)
			CACO A 1 Offset(0,48)
			CACO A 1 Offset(0,52)
			CACO A 1 Offset(0,56)
			CACO A 1 Offset(0,60)
			CACO A 1 Offset(0,68) A_FireCustomMissile("CacodemonBall_2")
		WaitFire:
			CACO A 1 Offset(0,76)
			CACO A 1 Offset(0,84)
			CACO A 1 Offset(0,92)
			CACO A 1 Offset(0,100)			
			CACO A 1 Offset(0,92)
			CACO A 1 Offset(0,84)
			CACO A 1 Offset(0,76)
			CACO A 1 Offset(0,68)
			CACO A 1 Offset(0,60)
			CACO A 1 Offset(0,52)
			CACO A 1 Offset(0,44)
			CACO A 1 Offset(0,38)
			Goto Ready
		AltFire:
			CACO A 1 Offset(0,34)
			CACO A 1 Offset(0,36)
			CACO A 1 Offset(0,38)
			CACO A 1 Offset(0,40)
			CACO A 1 Offset(0,44)
			CACO A 1 Offset(0,48)
			CACO A 1 Offset(0,52)
			CACO A 1 Offset(0,56)
			CACO A 1 Offset(0,60)
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 10, "Level4")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 6, "Level3")
			TNT1 A 0 A_JumpIfInventory("DiscipleSummons", 3, "Level2")
			Goto Fire+9
		Level2:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 3)
			CACO A 0 Offset(0,68) A_FireCustomMissile("CacodemonBall_2", -4)
			CACO A 1 Offset(0,68) A_FireCustomMissile("CacodemonBall_2", 4)
			Goto WaitFire
		Level3:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 6)
			CACO A 0 Offset(0,68) A_FireCustomMissile("CacodemonBall_2", -5)
			CACO A 0 Offset(0,68) A_FireCustomMissile("CacodemonBall_2")
			CACO A 1 Offset(0,68) A_FireCustomMissile("CacodemonBall_2", 5)
			Goto WaitFire
		Level4:
			TNT1 A 0 A_TakeInventory("DiscipleSummons", 10)
			CACO A 0 Offset(0,68) A_FireCustomMissile("CacodemonBall_3", -5)
			CACO A 0 Offset(0,68) A_FireCustomMissile("CacodemonBall_3")
			CACO A 1 Offset(0,68) A_FireCustomMissile("CacodemonBall_3", 5)
			Goto WaitFire

	}
}

Actor CacodemonBall_2 : CacodemonBall replaces CacodemonBall
{
	DamageType "Monster"
	Damage 10
	Speed 20
	SeeSound "doomcacodemon/attack"
	DeathSound "doomcacodemon/shotx"
}

Actor CacodemonBall_3 : CacodemonBall_2
{
	RenderStyle Normal
	Translation "217:223=231:239"

	Damage 20
	Speed 30
	States
	{
		Spawn:
			FAXE RS 4 Bright
			Loop
		Death:
			FAXE TUVWX 4 Bright
			Stop
	}
}