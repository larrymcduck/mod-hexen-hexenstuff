ACTOR ArcherClass : PlayerPawn
{
	Health 100
	PainChance 255
	Radius 16
	Height 64
	Speed 1
	PainSound "PlayerClericPain"
	RadiusDamageFactor 0.25
	Player.JumpZ 10.5
	Player.Viewheight 48
	Player.ColorRange 51, 66
	Player.SpawnClass "Cleric"
	Player.DisplayName "Archer"
	Player.SoundClass "cleric"
	Player.ScoreIcon "CLERFACE"
	Player.HealRadiusType "Mana"
	Player.Hexenarmor 10, 10, 15, 10, 15
	Player.InvulnerabilityMode "Ghost"
	Player.StartItem "ArcherBow"
	Player.StartItem "IsNotMonsterClass", 1
	Player.ForwardMove 1.2, 1.2
	Player.SideMove 1.2, 1.2
	Player.WeaponSlot 1, ArcherBow
	Player.WeaponSlot 2, ArcherCrossBow
	Player.WeaponSlot 3, ArcherIronCrossBow
	Player.WeaponSlot 4, ArcherDemonCrossBow
	
	States
	{
		Spawn:
			ARCP A -1
			loop
		See:
			ARCP ABCD 4
			Loop
		Missile:
		Melee:
			ARCP E 8
			Goto Spawn
		Pain:
			ARCP F 8 A_Pain
			Goto Spawn
		Death.MonsterIce:
		XDeath.MonsterIce:
		Death.Monster:
		XDeath.Monster:
			ARCP A 0 A_GiveToTarget("Health", 25)
			ARCP A 0 A_GiveToTarget("DiscipleSummons", 1)
			Goto Death
		Death:
		XDeath:
			ARCP G 6 A_PlayerScream
			ARCP H 6 A_NoBlocking
			ARCP IJKLMN 6
			ARCP O 6 A_SpawnItemEx("ReviveArcher")
			TNT1 A -1
			Stop
			
		Death.MonsterFire:
		XDeath.MonsterFire:
			ARCP A 0 A_GiveToTarget("Health", 25)
			ARCP A 0 A_GiveToTarget("DiscipleSummons", 1)
		Burn:
			ARCP P 6 Bright
			ARCP Q 6 Bright
			FDTH G 5 Bright
			FDTH H 4 Bright A_PlayerScream
			FDTH I 5 Bright
			FDTH J 4 Bright
			FDTH K 5 Bright
			FDTH L 4 Bright
			FDTH M 5 Bright
			FDTH N 4 Bright
			FDTH O 5 Bright
			FDTH P 4 Bright
			FDTH Q 5 Bright
			FDTH R 4 Bright
			FDTH S 5 Bright A_NoBlocking
			FDTH T 4 Bright
			FDTH U 5 Bright
			FDTH V 4 Bright
			TNT1 A 35 A_CheckPlayerDone
			Wait
			TNT1 A 8
			Stop
	}
}

Actor ArcherWeapon : Weapon
{
	Inventory.RestrictedTo "ArcherClass"
}

Actor ArcherBow : ArcherWeapon
{
	Weapon.SelectionOrder 3500
	Tag "Long Bow"
	
	States
	{
		Spawn:
			TNT1 A -1
			stop
		Ready:
			ABOW A 1 A_WeaponReady
			loop
		Deselect:
			ABOW A 1 A_Lower
			loop
		Select:
			ABOW A 1 A_Raise
			loop
		Fire:
			ABOW B 0 A_JumpIfInventory("IceArrowsAmmo",1,"ArrowIce")
			ABOW B 0 A_JumpIfInventory("FireArrowsAmmo",1,"ArrowFire")
			ABOW B 0 A_JumpIfInventory("PoisonArrowsAmmo",1,"ArrowPoison")
			ABOW B 0 A_JumpIfInventory("ExplosiveArrowsAmmo",1,"ArrowExplode")
			ABOW B 0 A_JumpIfInventory("RipperArrowsAmmo",3,"ArrowRip")
			ABOW BCD 4
			ABOW D 0 A_ReFire
		FireArrow:
			ABOW E 4 A_FireCustomMissile("ArcherArrow")
			ABOW FG 4
			Goto Ready
			
		ArrowIce:
			ABOW KLM 4
			ABOW D 0 A_ReFire
		FireArrowIce:
			ABOW D 0 A_TakeInventory("IceArrowsAmmo",1)
			ABOW E 4 A_FireCustomMissile("ArcherArrowIce")
			ABOW FG 4
			Goto Ready
			
		ArrowFire:
			ABOW NOP 4
			ABOW D 0 A_ReFire
		FireArrowFire:
			ABOW D 0 A_TakeInventory("FireArrowsAmmo",1)
			ABOW E 4 A_FireCustomMissile("ArcherArrowFire")
			ABOW FG 4
			Goto Ready
			
		ArrowPoison:
			ABOW HIJ 4
			ABOW D 0 A_ReFire
		FireArrowPoison:
			ABOW D 0 A_TakeInventory("PoisonArrowsAmmo",1)
			ABOW E 4 A_FireCustomMissile("ArcherArrowPoison")
			ABOW FG 4
			Goto Ready
			
		ArrowExplode:
			ABOW QRS 4
		FireArrowExplode:
			ABOW D 0 A_ReFire
			ABOW D 0 A_TakeInventory("ExplosiveArrowsAmmo",1)
			ABOW E 4 A_FireCustomMissile("ArcherArrowExplosive")
			ABOW FG 4
			Goto Ready
			
		ArrowRip:
			ABOW TUV 4
			ABOW D 0 A_ReFire
		FireArrowRip:
			ABOW D 0 A_TakeInventory("RipperArrowsAmmo",3)
			ABOW D 0 A_FireCustomMissile("ArcherArrowRipper",-6)
			ABOW D 0 A_FireCustomMissile("ArcherArrowRipper")
			ABOW E 4 A_FireCustomMissile("ArcherArrowRipper",6)
			ABOW FG 4
			Goto Ready

		Hold:
			ABOW B 0 A_JumpIfInventory("IceArrowsAmmo",1,"IceHold")
			ABOW B 0 A_JumpIfInventory("FireArrowsAmmo",1,"FireHold")
			ABOW B 0 A_JumpIfInventory("PoisonArrowsAmmo",1,"PoisonHold")
			ABOW B 0 A_JumpIfInventory("ExplosiveArrowsAmmo",1,"ExplodeHold")
			ABOW B 0 A_JumpIfInventory("RipperArrowsAmmo",3,"RipHold")
			ABOW D 1
			ABOW D 0 A_ReFire
			Goto FireArrow
		IceHold:
			ABOW M 1
			ABOW D 0 A_ReFire
			Goto FireArrowIce
		FireHold:
			ABOW P 1
			ABOW D 0 A_ReFire
			Goto FireArrowFire
		PoisonHold:
			ABOW J 1
			ABOW D 0 A_ReFire
			Goto FireArrowPoison
		ExplodeHold:
			ABOW S 1
			ABOW D 0 A_ReFire
			Goto FireArrowExplode
		RipHold:
			ABOW V 1
			ABOW D 0 A_ReFire
			Goto FireArrowRip
		AltFire:
			ABOW BCD 4
			ABOW D 0 A_ReFire
			Goto FireArrow
		AltHold:
			ABOW D 1
			ABOW D 0 A_ReFire
			Goto FireArrow
	}
}

Actor ArcherArrow
{
	Radius 4
	Height 8
	Speed 30
	Damage 5
	Projectile
	SeeSound "SorcererBallWoosh"
	States	
	{
		Spawn:
			ARRW A -1
			stop
		Death:
			ARRW A 1
			stop
	}
}

Actor ArcherArrowIce : ArcherArrow
{
	Damage 10
	DamageType "Ice"
	SeeSound "MageShardsFire"
	DeathSound "MageShardsExplode"
	States
	{
		Spawn:
			SHRD ABC 4 Bright
			loop
		Death:
			SHEX ABCDE 3 Bright
			stop
	}
}

Actor ArcherArrowFire : ArcherArrow
{
	Damage 10
	DamageType "Fire"
	SeeSound "ClericFlameFire"
	DeathSound "ClericFlameCircle"
	States
	{
		Spawn:
			CFFX N 4 Bright A_SpawnItemEx("ArcherArrowFireFX")
			loop
		Death:
			CFCF QRSTUVWXYZ 3 Bright
			stop
	}
}

Actor ArcherArrowFireFX
{
	+NOBLOCKMAP
	RenderStyle Add
	Gravity 0
	States
	{
		Spawn:
			CFFX OP 4 Bright
			stop
	}
}

Actor ArcherArrowPoison : ArcherArrow
{
	Damage 10
	PoisonDamage 10
	SeeSound "ClericCStaffFire"
	DeathSound "ClericCStaffExplode"
	States
	{
		Spawn:
			SSFX AB 4 Bright
			loop
		Death:
			SSFX CDEFGH 3 Bright
			stop
	}
}

Actor ArcherArrowExplosive : ArcherArrow
{
	Damage 15
	SeeSound "BishopAttack"
	DeathSound "MageStaffExplode"
	States
	{
		Spawn:
			FX12 AB 4 Bright
			loop
		Death:
			FX12 C 3 Bright A_Explode(64, 64, 0)
			FX12 DEFGH 3 Bright
			stop
	}
}

Actor ArcherArrowRipper : ArcherArrow
{
	Damage 2
	+RIPPER
	States
	{
		Spawn:
			BLAD A -1
			Stop
		Death:
			BLAD A 1
			stop
	}
}

Actor IceArrowsAmmo : Inventory
{
	Inventory.MaxAmount 30
}

Actor FireArrowsAmmo : Inventory
{
	Inventory.MaxAmount 30
}

Actor PoisonArrowsAmmo : Inventory
{
	Inventory.MaxAmount 30
}

Actor ExplosiveArrowsAmmo : Inventory
{
	Inventory.MaxAmount 30
}

Actor RipperArrowsAmmo : Inventory
{
	Inventory.MaxAmount 30
}

Actor ArcherArrows : CustomInventory
{
	Inventory.MaxAmount 25
	+Inventory.Invbar
	Inventory.RestrictedTo "ArcherClass"
}

Actor IceArrows : ArcherArrows 30008
{
	Inventory.Icon "ICEARW"
	Inventory.PickupMessage "Ice Arrows"

	States
	{
		Spawn:
			IARW A -1
			stop
		Use:
			TNT1 A 0 A_GiveInventory("IceArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("FireArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("PoisonArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("ExplosiveArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("RipperArrowsAmmo",30)
			stop
	}
}

Actor FireArrows : ArcherArrows 30009
{
	Inventory.Icon "FIRARW"
	Inventory.PickupMessage "Fire Arrows"

	States
	{
		Spawn:
			FARW A -1
			stop
		Use:
			TNT1 A 0 A_TakeInventory("IceArrowsAmmo",30)
			TNT1 A 0 A_GiveInventory("FireArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("PoisonArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("ExplosiveArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("RipperArrowsAmmo",30)
			stop
	}
}

Actor PoisonArrows : ArcherArrows 30010
{
	Inventory.Icon "POISARW"
	Inventory.PickupMessage "Poison Arrows"

	States
	{
		Spawn:
			PARW A -1
			stop
		Use:
			TNT1 A 0 A_TakeInventory("IceArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("FireArrowsAmmo",30)
			TNT1 A 0 A_GiveInventory("PoisonArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("ExplosiveArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("RipperArrowsAmmo",30)
			stop
	}
}

Actor ExplosiveArrows : ArcherArrows 30011
{
	Inventory.Icon "EXPARW"
	Inventory.PickupMessage "Explosive Arrows"

	States
	{
		Spawn:
			EARW A -1
			stop
		Use:
			TNT1 A 0 A_TakeInventory("IceArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("FireArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("PoisonArrowsAmmo",30)
			TNT1 A 0 A_GiveInventory("ExplosiveArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("RipperArrowsAmmo",30)
			stop
	}
}

Actor RipperArrows : ArcherArrows 30012
{
	Inventory.Icon "RIPARW"
	Inventory.PickupMessage "Ripper Arrows"

	States
	{
		Spawn:
			RARW A -1
			stop
		Use:
			TNT1 A 0 A_TakeInventory("IceArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("FireArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("PoisonArrowsAmmo",30)
			TNT1 A 0 A_TakeInventory("ExplosiveArrowsAmmo",30)
			TNT1 A 0 A_GiveInventory("RipperArrowsAmmo",30)
			stop
	}
}

Actor ArcherCrossBow : ArcherWeapon 200
{
	Radius 20
	Height 20
	Weapon.SelectionOrder 3400
	Weapon.AmmoUse 0
	Weapon.AmmoGive 15
	Weapon.AmmoType "Mana1"
	Inventory.PickupMessage "Ethereal Crossbow"
	Weapon.YAdjust 15
	Tag "Ethereal Bow"
	
	States
	{
		Spawn:
			CBW1 A -1
			stop
		Ready:
			CBOW AAAAAABBBBBBCCCCCC 1 A_WeaponReady
			loop
		Deselect:
			CBOW A 1 A_Lower
			loop
		Select:
			CBOW A 1 A_Raise
			loop
		Fire:
			CBOW D 0 A_JumpIfInventory("Mana1", 2,1)
			Goto Ready
			CBOW D 0 A_TakeInventory("Mana1", 2)
			CBOW D 0 A_FireCustomMissile("ArcherCrossBowArrow2",-4.5)
			CBOW D 0 A_FireCustomMissile("ArcherCrossBowArrowPow")
			CBOW D 0 A_FireCustomMissile("ArcherCrossBowArrow2",4.5)
			CBOW D 6 
			CBOW EFGH 3
			CBOW AB 4
			CBOW C 5 A_ReFire
			Goto Ready
		AltFire:
			CBOW D 0 A_JumpIfInventory("Mana1",5,1)
			Goto Fire
			CBOW D 0 A_TakeInventory("Mana1",5)
			CBOW D 0 A_FireCustomMissile("ArcherCrossBowArrow2",-9)
			CBOW D 0 A_FireCustomMissile("ArcherCrossBowArrowPow",-4.5)
			CBOW D 0 A_FireCustomMissile("ArcherCrossBowArrowPow")
			CBOW D 0 A_FireCustomMissile("ArcherCrossBowArrowPow",4.5)
			CBOW D 0 A_FireCustomMissile("ArcherCrossBowArrow2",9)
			CBOW D 6
			CBOW EFGH 3
			CBOW AB 4
			CBOW C 5 A_ReFire
			Goto Ready
	}
}

Actor ArcherCrossBowArrow : CrossbowFX1 replaces CrossbowFX1
{	
	+RIPPER
	SeeSound "ArcherSuperShot"
	DeathSound "ArcherSuperShotHit"
	States
	{
		Spawn:
			ARW2 A 1 Bright
			loop
		Death:
			ARW2 BCD 8 Bright
			stop
	}
}

Actor ArcherCrossBowArrowPow : ArcherCrossBowArrow
{	
	States
	{
		Spawn:
			ARW2 A 1 Bright A_SpawnItemEx("ArcherCrossBowArrowPowSparkle",random2[BoltSpark]()*0.015625,random2[BoltSpark]()*0.015625,0,0,0,0,0,SXF_ABSOLUTEPOSITION,50)
			loop
	}
}

Actor ArcherCrossBowArrowPowSparkle : CrossbowFX4
{
	States
	{
		Spawn:
			ARW2 EF 8 Bright
			stop
	}
}

Actor ArcherCrossBowArrow2 : CrossbowFX3 replaces CrossbowFX3
{
	DeathSound "ArcherSuperShotHit"
	States
	{
		Spawn:
			ARW3 A 1 Bright
			loop
		Death:
			ARW3 BCD 8 Bright
			stop
	}
}

Actor IronCrossBow : ArcherWeapon 201
{
	Radius 20
	Height 20

	Inventory.PickupMessage "Iron Crossbow"
	Weapon.SelectionOrder 3300
	Weapon.AmmoUse 0
	Weapon.AmmoGive 15
	Weapon.AmmoType "Mana2"
	Tag "Iron Crossbow"
	States
	{
		Spawn:
			ICBW A -1
			stop
		Ready:
			CBW2 A 1 A_WeaponReady
			loop
		Deselect:
			CBW2 A 1 A_Lower
			loop
		Select:
			CBW2 A 1 A_Raise
			loop
		Fire:
			CBW2 B 0 A_JumpIfInventory("Mana2", 4, 1)
			Goto Ready
			CBW2 B 0 A_TakeInventory("Mana2",4)
			CBW2 B 0 A_JumpIfInventory("IceArrowsAmmo",1,"FireArrowIce")
			CBW2 B 0 A_JumpIfInventory("FireArrowsAmmo",1,"FireArrowFire")
			CBW2 B 0 A_JumpIfInventory("PoisonArrowsAmmo",1,"FireArrowPoison")
			CBW2 B 0 A_JumpIfInventory("ExplosiveArrowsAmmo",1,"FireArrowExplode")
			CBW2 B 0 A_JumpIfInventory("RipperArrowsAmmo",3,"FireArrowRip")
		FireArrow:
			CBW2 B 0 A_FireCustomMissile("ArcherIronCrossBowArrow")
		DoneFiring:
			CBW2 B 6 Offset (0,40)
			CBW2 C 6 Offset (0,36)
			CBW2 A 6 Offset (0,32)
			Goto Ready
		FireArrowIce:
			CBW2 B 0 A_TakeInventory("IceArrowsAmmo",1)
			CBW2 B 0 A_FireCustomMissile("ArcherArrowIce2")
			Goto DoneFiring
		FireArrowFire:
			CBW2 B 0 A_TakeInventory("FireArrowsAmmo",1)
			CBW2 B 0 A_FireCustomMissile("ArcherArrowFire2")
			Goto DoneFiring
		FireArrowPoison:
			CBW2 B 0 A_TakeInventory("PoisonArrowsAmmo",1)
			CBW2 B 0 A_FireCustomMissile("ArcherArrowPoison2")
			Goto DoneFiring
		FireArrowExplode:
			CBW2 B 0 A_TakeInventory("ExplosiveArrowsAmmo",1)
			CBW2 B 0 A_FireCustomMissile("ArcherArrowExplosive2")
			Goto DoneFiring
		FireArrowRip:
			CBW2 B 0 A_TakeInventory("RipperArrowsAmmo",3)
			CBW2 B 0 A_FireCustomMissile("ArcherArrowRipper2",-6)
			CBW2 B 0 A_FireCustomMissile("ArcherArrowRipper2")
			CBW2 B 0 A_FireCustomMissile("ArcherArrowRipper2",6)
			Goto DoneFiring
		AltFire:
			CBW2 B 0 A_JumpIfInventory("Mana2",4,1)
			Goto Ready
			CBW2 B 0 A_TakeInventory("Mana2",4)
			Goto FireArrow
	}
}

Actor ArcherIronCrossBowArrow
{
	Radius 6
	Height 12
	Speed 40
	Damage 10
	Projectile
	SeeSound "SorcererBallWoosh"
	States
	{
		Spawn:
			XBLT A -1
			stop
		Death:
			XBLT A 1
			stop
	}
}

Actor ArcherArrowIce2 : ArcherArrowIce
{
	Damage 15
	States
	{
		Spawn:
			ICPR ABC 4 Bright
			loop
		Death:
			ICPR DEFGH 3 Bright
			stop
	}
}

Actor ArcherArrowFire2 : ArcherArrowFire
{
	Damage 15
	DamageType "Fire"
	SeeSound "ClericFlameFire"
	DeathSound "MageStaffExplode"
	States
	{
		Spawn:
			DMFX ABC 4 Bright
			loop
		Death:
			DMFX DEFGH 3 Bright
			stop
	}
}

Actor ArcherArrowPoison2 : ArcherArrowPoison
{
	Damage 15
	PoisonDamage 10
	SeeSound "ClericCStaffFire"
	DeathSound "ClericCStaffExplode"
	States
	{
		Spawn:
			D2FX ABCDEF 4 Bright
			loop
		Death:
			D2FX GHIJKL 3 Bright
			stop
	}
}

Actor ArcherArrowExplosive2 : ArcherArrowExplosive
{
	Damage 30
	SeeSound "BishopAttack"
	DeathSound "MageStaffExplode"
	States
	{
		Spawn:
			SPIR P 4 Bright
			loop
		Death:
			FHFX IJK 3 Bright A_Explode
			FHFX LMNOPQRSTUVW 3 Bright
			stop
	}
}

Actor ArcherArrowRipper2 : ArcherArrowRipper
{
	Damage 10
	+RIPPER
	States
	{
		Spawn:
			RIPP ABC 3
			loop
		Death:
			CFCF QRSTUVWXYZ 3
			stop
	}
}

Actor ArcherPiece : CustomInventory
{
	+FLOATBOB
	+NOGRAVITY
	Inventory.PickupSound "PickupWeapon"
	Inventory.PickupMessage "Demon Crossbow Piece"
	Inventory.RestrictedTo "ArcherClass"
}

Actor ArcherWeaponPiece : WeaponPiece
{
	Radius 20
	Height 20	
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Demon Crossbow Piece"
	WeaponPiece.Weapon ArcherDemonCrossBow
	Inventory.RestrictedTo "ArcherClass"
	+FLOATBOB
	+NOGRAVITY
}

Actor AWeaponPiece1 : ArcherWeaponPiece 30000
{
	WeaponPiece.Number 1
	States
	{
		Spawn:
			DBOW J -1 Bright
			stop
	}
}

Actor AWeaponPiece2 : ArcherWeaponPiece 30001
{
	WeaponPiece.Number 2
	States
	{
		Spawn:
			DBOW K -1 Bright
			stop
	}
}

Actor AWeaponPiece3 : ArcherWeaponPiece 30002
{
	WeaponPiece.Number 3
	States
	{
		Spawn:
			DBOW L -1 Bright
			stop
	}
}

Actor ArcherDemonCrossBow : ArcherWeapon
{
	Weapon.SelectionOrder 3200
	Health 3
	+Weapon.Primary_Uses_Both
	Weapon.AmmoUse1 15
	Weapon.AmmoUse2 15
	Weapon.AmmoGive1 20
	Weapon.AmmoGive2 20
	Weapon.AmmoType1 "Mana1"
	Weapon.AmmoType2 "Mana2"
	Inventory.PickupMessage "Demon CrossBow Assembled"
	Inventory.PickupSound "WeaponBuild"
	Tag "Demon Crossbow"
	
	States
	{
		Spawn:
			TNT1 A -1
			stop
		Ready:
			DBOW A 1 A_WeaponReady
			loop
		Select:
			DBOW A 1 A_Raise
			loop
		Deselect:
			DBOW A 1 A_Lower
			loop
		Fire:
			DBOW BCD 3 Bright
			DBOW B 0 Bright A_PlaySound("MageStaffFire")
			DBOW E 4 Bright Offset (0,40) A_FireCustomMissile("ArcherDemonCrossBowArrow")
			DBOW F 4 Bright Offset (0,36)
			DBOW G 4 Bright Offset (0,32)			
			Goto Ready
		AltFire:
			DBOW BCDHDC 2 Bright A_JumpIfCloser(96,"Drain")
			Goto Ready
		Drain:
			DBOW I 0 A_GiveInventory("ArcherDemonBowDrain", 1)
			DBOW I 10 Bright A_CustomPunch(10,0,0,"WraithPuff",96)
			Goto Ready
	}
}

Actor ArcherDemonBowDrain : PowerupGiver
{
	+INVENTORY.AUTOACTIVATE
	Inventory.MaxAmount 0
	Powerup.Duration 2
	Powerup.Type Drain
}

Actor ArcherDemonCrossBowArrow
{
	Height 8
	Radius 16
	Speed 50
	Damage 30
	Projectile
	SeeSound "MageStaffFire"
	DeathSound "MageStaffExplode"
	States
	{
		Spawn:
			DBWF A 1 Bright A_SpawnItemEx("ArcherDemonCrossBowArrowMagic")
			loop
		Death:
			DBWF B 3 Bright A_Explode(128, 128, 0)
			DBWF CDEFG 3 Bright
			stop
	}
}

Actor ArcherDemonCrossBowArrowMagic : CrossbowFX4
{
	States
	{
		Spawn:
			DBWF HIJKL 3 Bright
			stop
	}
}