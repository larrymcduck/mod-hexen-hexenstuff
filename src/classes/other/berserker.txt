ACTOR BerserkerClass : PlayerPawn
{
	Health 100
	PainChance 255
	Radius 16
	Height 64
	Speed 1
	+DONTRIP
	+DONTBLAST
	BloodColor "ff 88 00" 
	PainSound "EttinPain"
	RadiusDamageFactor 0.25
	Player.JumpZ 9
	Player.Viewheight 56
	Player.ColorRange 0, 32
	Player.SpawnClass "Fighter"
	Player.DisplayName "Berserker"
	Player.SoundClass "ettin"
	Player.ScoreIcon "FITEFACE"
	Player.HealRadiusType "Armor"
	Player.Hexenarmor 25, 35, 30, 25, 15
	Player.StartItem "EttinMaceWeapon"
	Player.StartItem "EttinHealth", 100
	Player.StartItem "IsNotMonsterClass", 1
	Player.ForwardMove 0.64, 0.64
	Player.SideMove 0.64, 0.64
	Player.WeaponSlot 1, EttinMaceWeapon
	Player.WeaponSlot 2, DoubleBladedSword
	Player.WeaponSlot 3, BerserkerAxe
	Player.WeaponSlot 4, VorpalSword
	
	States
	{
		Spawn:
			CETN A -1
			loop
		See:
			CETN ABCD 6
			Loop
		Missile:
		Melee:
			CETN EFG 5
			Goto Spawn
		Pain:
			CETN H 8 A_Pain
			Goto Spawn
		Death.MonsterIce:
		XDeath.MonsterIce:
			CETN A 0 A_GiveToTarget("Health", 25)
			CETN A 0 A_GiveToTarget("DiscipleSummons", 1)
		Ice:
			CETX L 5 A_FreezeDeath
			CETX L 1 A_FreezeDeathChunks
			wait
		XDeath.MonsterFire:
		XDeath.Monster:
			CETN A 0 A_GiveToTarget("Health", 25)
			CETN A 0 A_GiveToTarget("DiscipleSummons", 1)
		XDeath:
			CETX A 4 A_NoBlocking
			CETX B 4 A_SpawnItemEx("EttinMace", 0, 0, 8.5, random[DropMace](-128, 127)*0.03125, random[DropMace](-128, 127)*0.03125, 10+random[DropMace](0,255)*0.015625, 0)
			CETX C 4 A_PlayerScream
			CETX D 4 
			CETX EFGHIJ 4
			CETX K 4 A_SpawnItemEx("XReviveBerserker")
			TNT1 A -1
			Stop
		Death.MonsterFire:
		Death.Monster:
			CETX A 0 A_GiveToTarget("Health", 25)
			CETX A 0 A_GiveToTarget("DiscipleSummons", 1)
		Death:
			CETN I 4
			CETN J 4 A_PlayerScream
			CETN K 4
			CETN L 4 A_NoBlocking
			CETN M 4
			CETN N 4
			CETN O 4
			CETN P 4
			CETN Q 4 A_SpawnItemEx("ReviveBerserker")
			TNT1 A -1
			Stop
	}
}

Actor EttinHealth : UpgradeStamina
{
	Inventory.MaxAmount 100
}

Actor BerserkerWeapon : Weapon
{
	+BLOODSPLATTER
	+WEAPON.MELEEWEAPON
	Inventory.RestrictedTo "BerserkerClass"
}

Actor EttinMaceWeapon : BerserkerWeapon
{
	+BLOODSPLATTER
	Weapon.SelectionOrder 3500
	Weapon.KickBack 150
	Weapon.YAdjust -12
	Tag "Mace"
	
	States
	{
		Select:	
			ETMC A 1 A_Raise
			loop
		Deselect:
			ETMC A 1 A_Lower
			loop
		Ready:
			ETMC A 1 A_WeaponReady
			loop
		Fire:
			ETMC B 4 Offset(15,32)
			ETMC C 3 Offset(15,32)
			ETMC D 2 Offset(15,32)
			ETMC D 1 Offset(-5, 70) A_CustomPunch(12,0,0,"HammerPuff",128)
			ETMC D 2 Offset(-25, 90)
			ETMC E 1 Offset(15, 32)
			ETMC E 2 Offset(10, 54)
			ETMC E 7 Offset(10, 150)
			ETMC A 1 Offset(0, 60) A_ReFire
			ETMC A 1 Offset(0, 52)
			ETMC A 1 Offset(0, 44)
			ETMC A 1 Offset(0, 36)
			ETMC A 1 
			Goto Ready
		AltFire:
			ETMC B 10 Offset(15,32)
			ETMC C 3 Offset(15,32)
			ETMC D 2 Offset(15,32)
			ETMC D 1 Offset(-5, 70) A_CustomPunch(20,0,0,"HammerPuff",128)
			ETMC D 2 Offset(-25, 90)
			ETMC E 1 Offset(15, 32)
			ETMC E 2 Offset(10, 54)
			ETMC E 7 Offset(10, 150)
			ETMC A 1 Offset(0, 60) A_ReFire
			ETMC A 1 Offset(0, 52)
			ETMC A 1 Offset(0, 44)
			ETMC A 1 Offset(0, 36)
			ETMC A 1 
			Goto Ready
	}
}

Actor DoubleBladedSword : BerserkerWeapon 30017
{
	+WEAPON.AMMO_OPTIONAL
	Weapon.SelectionOrder 3400
	Weapon.AmmoType1 "Mana1"
	Weapon.AmmoGive1 20
	Weapon.AmmoUse1 5
	Weapon.KickBack 150
	Weapon.YAdjust 10
	Inventory.PickupMessage "Double Bladed Skull Sword"
	Tag "Double Bladed Skull Sword"
	
	States
	{
		Spawn:
			ETSW A -1
			stop
		Select:
			ETSW B 1 A_Raise
			loop
		Deselect:
			ETSW B 1 A_Lower
			loop
		Ready:
			ETSW B 1 A_WeaponReady
			loop
		Fire:
			TNT1 A 3
			ETSW C 3 Offset(5,36)
			ETSW D 2 Offset(5,36)
			ETSW D 0 A_JumpIfInventory("Mana1", 5, 1)
			Goto AltFire + 3
			ETSW E 3 Offset(5,36) A_CustomPunch(30,0,1,"EttinSwordPuff",144)
			ETSW F 2 Offset(5,36)
			TNT1 A 12
			TNT1 A 0 A_ReFire
			Goto Done
		AltFire:
			TNT1 A 3
			ETSW C 3 Offset(5,36)
			ETSW D 2 Offset(5,36)
			ETSW E 3 Offset(5,36) A_CustomPunch(20,0,0,"HammerPuff",144)
			ETSW F 2 Offset(5,36)
			TNT1 A 12
			TNT1 A 0 A_ReFire
			Goto Done
		Done:
			ETSW B 1 Offset(5,60)
			ETSW B 1 Offset(5,55)
			ETSW B 1 Offset(5,50)
			ETSW B 1 Offset(5,45)
			ETSW B 1 Offset(5,40)
			Goto Ready
	}
}

Actor EttinSwordPuff : AxePuffGlow
{
	Translation "0:255=164:185"
}

Actor BerserkerAxe : BerserkerWeapon 30018
{
	Scale 1.2
	+WEAPON.AMMO_OPTIONAL
	Weapon.SelectionOrder 3300
	Weapon.AmmoType1 "Mana2"
	Weapon.AmmoGive1 20
	Weapon.AmmoUse1 5
	Weapon.KickBack 150
	Weapon.YAdjust -12
	
	Inventory.PickupMessage "Double Bladed Axe"
	Tag "Double Bladed Axe"
	
	States
	{
		Spawn:
			EAXE A -1
			stop
		Select:
			EAXE B 1 A_Raise
			loop
		Deselect:
			EAXE B 1 A_Lower
			loop
		Ready:
			EAXE B 1 A_WeaponReady
			loop
		Fire:
			TNT1 A 0 A_SetReflective
			EAXE C 4 Offset(15,32)
			EAXE D 3 Offset(15,32)
			EAXE E 2 Offset(15,32)
			ETSW D 0 A_JumpIfInventory("Mana2", 5, 1)
			Goto AltFire + 4
			EAXE E 1 Offset(-5, 70) A_CustomPunch(50,0,1,"EttinAxePuff",144)
			EAXE E 2 Offset(-25, 90)
			EAXE F 1 Offset(15, 32)
			EAXE F 2 Offset(10, 54)
			EAXE F 7 Offset(10, 150)
			EAXE A 0 Offset(0, 60) A_ReFire
			Goto Done
		AltFire:
			TNT1 A 0 A_SetReflective
			EAXE C 4 Offset(15,32)
			EAXE D 3 Offset(15,32)
			EAXE E 2 Offset(15,32)
			EAXE E 1 Offset(-5, 70) A_CustomPunch(30,0,0,"AxePuff",144)
			EAXE E 2 Offset(-25, 90)
			EAXE F 1 Offset(15, 32)
			EAXE F 2 Offset(10, 54)
			EAXE F 7 Offset(10, 150)
			EAXE A 0 Offset(0, 60) A_ReFire
			Goto Done
		Done:
			TNT1 A 0 A_UnSetReflective
			EAXE B 1 Offset(0, 60)
			EAXE B 1 Offset(0, 52)
			EAXE B 1 Offset(0, 44)
			EAXE B 1 Offset(0, 36)
			EAXE B 1 
			Goto Ready
	}
}

Actor EttinAxePuff : AxePuffGlow
{
	States
	{
		Spawn:
			DMFX EFGH 4 Bright
			stop
	}
}

Actor EttinWeaponPiece : WeaponPiece
{
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Vorpal Sword Piece"
	WeaponPiece.Weapon VorpalSword
	Inventory.RestrictedTo "BerserkerClass"
	+FLOATBOB
	+NOGRAVITY
}

Actor EttinWeaponPiece1 : EttinWeaponPiece 30019
{
	WeaponPiece.Number 1
	States
	{
		Spawn:
			ESW2 C -1 Bright
			stop
	}
}

Actor EttinWeaponPiece2 : EttinWeaponPiece 30020
{
	WeaponPiece.Number 2
	States
	{
		Spawn:
			ESW2 B -1 Bright
			stop
	}
}

Actor EttinWeaponPiece3 : EttinWeaponPiece 30021
{
	WeaponPiece.Number 3
	States
	{
		Spawn:
			ESW2 A -1 Bright
			stop
	}
}

Actor VorpalSword : BerserkerWeapon
{
	Health 3
	Weapon.AmmoUse1 10
	Weapon.AmmoUse2 10
	Weapon.AmmoGive1 20
	Weapon.AmmoGive2 20
	Weapon.KickBack 150
	Weapon.AmmoType1 "Mana1"
	Weapon.AmmoType2 "Mana2"
	Weapon.YAdjust 10
	+WEAPON.PRIMARY_USES_BOTH
	+WEAPON.AMMO_OPTIONAL
	+INVENTORY.NOATTENPICKUPSOUND
	Weapon.SelectionOrder 3200
	Inventory.PickupMessage "Vorpal Sword Assembled"
	
	Tag "Vorpal Sword"
	
	States
	{
		Spawn:
			TNT1 A -1
			stop
		Select:
			ESW2 D 1 A_Raise
			loop
		Deselect:
			ESW2 D 1 A_Lower
			loop
		Ready:
			ESW2 D 1 A_WeaponReady
			loop
		Fire:
			ESW2 E 0 Offset(5,36) A_SetReflective
			ESW2 EF 3 Offset(5,36)
			ESW2 G 2 Offset(5,36)
			ESW2 G 0 A_JumpIfInventory("Mana1", 10, 1)
			Goto AltFire+3
			ESW2 G 0 A_JumpIfInventory("Mana2", 10, 1)
			Goto AltFire+3
			ESW2 H 3 Offset(5,36) A_CustomPunch(80,0,1,"EttinSword2Puff",164)
			ESW2 I 2 Offset(5,36)
			ESW2 J 2 Offset(5,36)
			ESW2 J 10 Offset(5,150)
			ESW2 J 0 A_ReFire
			Goto Done
		AltFire:
			ESW2 E 0 Offset(5,36) A_SetReflective
			ESW2 EF 3 Offset(5,36)
			ESW2 G 2 Offset(5,36)
			ESW2 H 3 Offset(5,36) A_CustomPunch(50,0,0,"AxePuff",164)
			ESW2 I 2 Offset(5,36)
			ESW2 J 2 Offset(5,36)
			ESW2 J 10 Offset(5,150)
			ESW2 J 0 A_ReFire
			Goto Done
		Done:
			ESW2 D 0 A_UnsetReflective
			ESW2 D 1 Offset(5,60)
			ESW2 D 1 Offset(5,55)
			ESW2 D 1 Offset(5,50)
			ESW2 D 1 Offset(5,45)
			ESW2 D 1 Offset(5,40)
			Goto Ready
	}
}

Actor EttinSword2Puff : AxePuff
{
	States
	{
		Spawn:
			MSP2 EFGHI 3 Bright
			stop
	}
}